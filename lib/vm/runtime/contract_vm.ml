(* SPDX-License-Identifier: BSD-3-Clause *)
(* Copyright (c) 2023-2026 Octra Labs <dev@octra.org> *)

type v =
  | VInt of Z.t
  | VBool of bool
  | VString of string
  | VBytes of string
  | VBytes32 of string
  | VU64 of Z.t
  | VU128 of Z.t
  | VU256 of Z.t
  | VAddr of string
  | VCipher of Pvac_ffi.cipher
  | VPubKey of Pvac_ffi.pubkey

type reg = int

type instr =
  | ADD of reg * reg * reg
  | SUB of reg * reg * reg
  | MUL of reg * reg * reg
  | DIV of reg * reg * reg
  | MOD of reg * reg * reg
  | NEG of reg * reg
  | ABS of reg * reg
  | EQ of reg * reg * reg
  | LT of reg * reg * reg
  | GT of reg * reg * reg
  | NEQ of reg * reg * reg
  | LDI of reg * v
  | MOV of reg * reg
  | SLOAD of reg * string
  | SSTORE of string * reg
  | SDEL of string
  | SLOADK of reg * reg
  | SSTOREK of reg * reg
  | SDELK of reg
  | MLOAD of reg * int
  | MSTORE of int * reg
  | JMP of int
  | JIF of reg * int
  | JDEST of int
  | STOP
  | REVERT
  | CALLER of reg
  | ORIGIN of reg
  | SELF of reg
  | EPOCH of reg
  | EPOCH_TIME of reg
  | VALUE of reg
  | BALANCE of reg * reg
  | TREEHASH of reg
  | NODEID of reg
  | TXHASH of reg
  | XCALL of reg * reg * reg * reg * int
  | SPAWN of reg * reg
  | SPAWN2 of reg * reg * reg * int
  | TRANSFER of reg * reg * reg
  | CHECKPOINT
  | ROLLBACK
  | COMMIT
  | EMIT of string * reg list
  | CONCAT of reg * reg * reg
  | STRLEN of reg * reg
  | ASSERT of reg
  | EFFORT of reg
  | NOP
  | FHE_LOAD_PK of reg * reg
  | FHE_ADD of reg * reg * reg * reg
  | FHE_SUB of reg * reg * reg * reg
  | FHE_MUL of reg * reg * reg * reg
  | FHE_SCALE of reg * reg * reg * reg
  | FHE_DIV_CONST of reg * reg * reg * reg
  | FHE_ADD_CONST of reg * reg * reg * reg
  | FHE_SUB_CONST of reg * reg * reg * reg
  | FHE_VERIFY_ZERO of reg * reg * reg * reg
  | FHE_VERIFY_RANGE of reg * reg * reg * reg
  | FHE_VERIFY_BOUND of reg * reg * reg * reg * reg
  | GROTH16_VERIFY_BN254 of reg * reg * reg * reg
  | FHE_COMMIT of reg * reg * reg
  | FHE_PEDERSEN of reg * reg * reg
  | FHE_SER of reg * reg
  | FHE_DESER of reg * reg
  | FHE_SER_PK of reg * reg
  | FHE_DESER_PK of reg * reg
  | CALL_INT of reg * int
  | MLOADR of reg * reg
  | MSTORER of reg * reg
  | PARSE_INTS of reg * reg * reg
  | ISADDR of reg * reg
  | ISHEX of reg * reg
  | STATE_PATH_KEY of reg * reg
  | OBJECT_MEMBER_COUNT of reg * reg
  | OBJECT_HAS_MEMBER of reg * reg * reg
  | OBJECT_MEMBER_REF_AT of reg * reg * reg
  | OBJECT_TRANSITION_APPLY of reg * reg * reg * reg * reg * reg * reg * reg * reg * reg * reg
  | ASSERT_ADDR of reg
  | SUBSTR of reg * reg * reg * reg
  | INDEXOF of reg * reg * reg
  | SHA256 of reg * reg
  | KECCAK256 of reg * reg
  | ED25519_OK of reg * reg * reg * reg
  | BITAND of reg * reg * reg
  | BITOR of reg * reg * reg
  | BITXOR of reg * reg * reg
  | BITSHL of reg * reg * reg
  | BITSHR of reg * reg * reg
  | SKEYS of reg * reg * reg
  | SKEYS_PAGE of reg * reg * reg * reg * reg
  | SLOADN of reg * reg * reg
  | SSTOREN of reg * reg * reg
  | FSTORE of reg * reg
  | FLOAD of reg * reg
  | MATMUL of reg * reg * reg * reg * reg * reg
  | VECDOT of reg * reg * reg * reg
  | VECDOT_Q16 of reg * reg * reg * reg
  | EXP_LUT of reg * reg
  | EXP_Q16 of reg * reg
  | SOFTMAX_INPLACE of reg * reg
  | SOFTMAX_Q16_INPLACE of reg * reg
  | LAYERNORM_INPLACE of reg * reg * reg * reg
  | LAYERNORM_Q16_INPLACE of reg * reg * reg * reg
  | RELU_INPLACE of reg * reg
  | RMSNORM_INPLACE of reg * reg * reg
  | RMSNORM_Q16_INPLACE of reg * reg * reg
  | SILU_INPLACE of reg * reg
  | SILU_Q16_INPLACE of reg * reg
  | ELEMWISE_MUL_INPLACE of reg * reg * reg
  | ELEMWISE_MUL_Q16 of reg * reg * reg
  | LOAD_INT8_BYTES_TO_MEM of reg * reg * reg * reg * reg
  | RESIDUAL_ADD of reg * reg * reg
  | RESIDUAL_ADD_Q16 of reg * reg * reg
  | ROPE_APPLY of reg * reg * reg * reg
  | ROPE_APPLY_Q16 of reg * reg * reg * reg
  | LOAD_INT8_B64_TO_MEM of reg * reg * reg * reg * reg
  | LOAD_INT8_Q16 of reg * reg * reg * reg * reg
  | APPEND_VEC_Q16 of reg * reg * reg * reg
  | ARGMAX_Q16 of reg * reg * reg
  | MATMUL_Q16 of reg * reg * reg * reg * reg * reg
  | LINEAR_Q1_G128_FP of reg * reg * reg * reg * reg * reg * reg
  | LOAD_F32_LE_FP of reg * reg * reg * reg
  | LOAD_F64_LE_FP of reg * reg * reg * reg
  | SIGMOID_FP of reg * reg
  | SOFTPLUS_FP of reg * reg
  | CAUSAL_DEPTHWISE_CONV1D_FP of reg * reg * reg * reg * reg * reg
  | GATED_DELTA_RULE_FP of
      reg * reg * reg * reg * reg * reg * reg * reg * reg * reg * reg * reg * reg * reg
  | SHIFT_ROUND_INPLACE of reg * reg * reg
  | MATMUL_FP of reg * reg * reg * reg * reg * reg
  | RMSNORM_FP of reg * reg * reg
  | RMSNORM_FP_EPS of reg * reg * reg * reg
  | L2NORM_FP of reg * reg * reg
  | SILU_FP of reg * reg
  | ELEMWISE_MUL_FP of reg * reg * reg
  | RESIDUAL_ADD_FP of reg * reg * reg
  | ROPE_APPLY_FP of reg * reg * reg * reg
  | ROPE_APPLY_INDEXED_FP of reg * reg * reg * reg * reg * reg
  | LOAD_INT8_FP of reg * reg * reg * reg * reg
  | VECDOT_FP of reg * reg * reg * reg
  | ARGMAX_FP of reg * reg * reg
  | ATTENTION_SCORES_FP of reg * reg * reg * reg * reg
  | SOFTMAX_FP of reg * reg * reg
  | ATTENTION_WEIGHTED_SUM_FP of reg * reg * reg * reg * reg
  | ATTENTION_KV_FP of reg * reg * reg * reg * reg * reg * reg * reg
  | ATTENTION_KV_Q16 of reg * reg * reg * reg * reg * reg * reg * reg
  | APPEND_VEC_FP of reg * reg * reg * reg

type mem = { mutable data : (int, v) Hashtbl.t; mutable size : int }

type undo_entry =
  | UndoMarker of int
  | UndoWrite of string * string option

type event_record = {
  contract : string;
  depth : int;
  event : string;
  values : v list;
}

type subcall_result = {
  return_value : v;
  effort_used : int;
  events : event_record list;
}

type spawn_result = {
  spawned_addr : string;
  effort_used : int;
  events : event_record list;
}

type fhe_capability =
  | Fhe_load_pk_cap
  | Fhe_encrypt_cap
  | Fhe_decrypt_cap
  | Fhe_cipher_arithmetic_cap
  | Fhe_verify_zero_cap
  | Fhe_verify_range_cap
  | Fhe_verify_bound_cap
  | Fhe_commit_cap
  | Fhe_pedersen_cap
  | Fhe_cipher_serde_cap
  | Fhe_pubkey_serde_cap

type exec_ctx = {
  get_balance : string -> Z.t;
  do_transfer : string -> string -> Z.t -> bool;
  call_contract : string -> string -> string -> v list -> int -> (subcall_result, string) result;
  deploy_contract : string -> string -> int -> int -> v list -> (spawn_result, string) result;
  get_fhe_pubkey : string -> Pvac_ffi.pubkey option;
  get_fhe_keypair : string -> (Pvac_ffi.pubkey * Pvac_ffi.seckey) option;
  allow_fhe_capability : fhe_capability -> bool;
  circle_hfhe_key_id : string option;
  circle_hfhe_intent_id : string option;
  circle_hfhe_active_relay_id : string option;
  current_epoch : int;
  epoch_time_ms : int64;
  tree_hash : string;
  node_id : string;
  tx_hash : string;
}

let default_ctx = {
  tx_hash = String.make 64 '0';
  get_balance = (fun _ -> Z.zero);
  do_transfer = (fun _ _ _ -> false);
  call_contract = (fun _ _ _ _ _ -> Error "not implemented");
  deploy_contract = (fun _ _ _ _ _ -> Error "not implemented");
  get_fhe_pubkey = (fun _ -> None);
  get_fhe_keypair = (fun _ -> None);
  allow_fhe_capability = (fun _ -> true);
  circle_hfhe_key_id = None;
  circle_hfhe_intent_id = None;
  circle_hfhe_active_relay_id = None;
  current_epoch = 0;
  epoch_time_ms = 0L;
  tree_hash = String.make 64 '0';
  node_id = "node_001";
}

type storage_kind =
  | StorageInt
  | StorageBool
  | StorageString
  | StorageBytes
  | StorageBytes32
  | StorageU64
  | StorageU128
  | StorageU256
  | StorageAddr

type s = {
  regs : v array;
  mutable memory : mem;
  storage : (string, string) Hashtbl.t;
  mutable effort_used : int;
  effort_limit : int;
  mutable reverted : bool;
  mutable pc : int;
  caller : string;
  origin : string;
  address : string;
  value : Z.t;
  logs : event_record list ref;
  ctx : exec_ctx;
  mutable undo_stack : undo_entry list;
  mutable undo_id : int;
  mutable call_depth : int;
  mutable return_stack : (int * int * v array) list;
  blobs : (string, string) Hashtbl.t;
  mutable is_view : bool;
  strict_values : bool;
  storage_kinds : (string, storage_kind) Hashtbl.t;
  strict_blobs : bool;
  decoded_chunk_cache : (int, string) Hashtbl.t;
}

let max_storage_value_len = 4_194_304

let is_reserved_key k =
  String.length k > 0 && Char.code k.[0] = 0

let effort_cost = function
  | LDI _ | MOV _ | JMP _ | JDEST _ | STOP | REVERT | NOP | EFFORT _ | CALL_INT _ -> 1
  | EQ _ | LT _ | GT _ | NEQ _ -> 2
  | CALLER _ | ORIGIN _ | SELF _ | EPOCH _ | EPOCH_TIME _ | VALUE _ -> 2
  | ADD _ | SUB _ | MUL _ | MLOAD _ | MSTORE _ | MLOADR _ | MSTORER _ | CONCAT _ | STRLEN _ | ISADDR _ | ISHEX _ | STATE_PATH_KEY _ -> 3
  | DIV _ | MOD _ | NEG _ | ABS _ -> 5
  | OBJECT_HAS_MEMBER _ -> 30
  | OBJECT_MEMBER_COUNT _ | OBJECT_MEMBER_REF_AT _ -> 50
  | JIF _ | ASSERT _ | ASSERT_ADDR _ | TREEHASH _ | NODEID _ | TXHASH _ -> 5
  | SLOAD _ | SLOADK _ | BALANCE _ -> 20
  | EMIT _ -> 30
  | SDEL _ | SDELK _ | TRANSFER _ -> 50
  | SSTORE _ | SSTOREK _ | XCALL _ | CHECKPOINT -> 100
  | ROLLBACK -> 200
  | SPAWN _ | SPAWN2 _ -> 5000
  | COMMIT -> 10
  | FHE_SER_PK _ | FHE_DESER_PK _ -> 50
  | FHE_LOAD_PK _ | FHE_SER _ | FHE_DESER _ -> 100
  | FHE_COMMIT _ | FHE_PEDERSEN _ -> 200
  | FHE_ADD _ | FHE_SUB _ | FHE_ADD_CONST _ | FHE_SUB_CONST _ -> 500
  | FHE_SCALE _ | FHE_DIV_CONST _ -> 1000
  | FHE_MUL _ -> 10000
  | FHE_VERIFY_ZERO _ | FHE_VERIFY_RANGE _ | FHE_VERIFY_BOUND _ -> 5_000_000
  | PARSE_INTS _ -> 10
  | SUBSTR _ -> 5
  | INDEXOF _ -> 10
  | SHA256 _ | KECCAK256 _ -> 20
  | ED25519_OK _ -> 2000
  | BITAND _ | BITOR _ | BITXOR _ | BITSHL _ | BITSHR _ -> 3
  | SKEYS _ -> 50
  | SKEYS_PAGE _ -> 50
  | SLOADN _ -> 50
  | SSTOREN _ -> 100
  | FSTORE _ -> 100
  | FLOAD _ -> 100
  | GROTH16_VERIFY_BN254 _ -> 50000
  | OBJECT_TRANSITION_APPLY _ -> 300
  | MATMUL _ -> 100
  | VECDOT _ -> 10
  | VECDOT_Q16 _ -> 10
  | EXP_LUT _ -> 5
  | EXP_Q16 _ -> 8
  | SOFTMAX_INPLACE _ -> 20
  | SOFTMAX_Q16_INPLACE _ -> 25
  | LAYERNORM_INPLACE _ -> 30
  | LAYERNORM_Q16_INPLACE _ -> 30
  | RELU_INPLACE _ -> 5
  | RMSNORM_INPLACE _ -> 25
  | RMSNORM_Q16_INPLACE _ -> 25
  | SILU_INPLACE _ -> 10
  | SILU_Q16_INPLACE _ -> 10
  | ELEMWISE_MUL_INPLACE _ -> 5
  | ELEMWISE_MUL_Q16 _ -> 5
  | LOAD_INT8_BYTES_TO_MEM _ -> 10
  | RESIDUAL_ADD _ -> 5
  | RESIDUAL_ADD_Q16 _ -> 5
  | ROPE_APPLY _ -> 50
  | ROPE_APPLY_Q16 _ -> 50
  | LOAD_INT8_B64_TO_MEM _ -> 30
  | LOAD_INT8_Q16 _ -> 30
  | APPEND_VEC_Q16 _ -> 5
  | ARGMAX_Q16 _ -> 5
  | MATMUL_Q16 _ -> 100
  | LINEAR_Q1_G128_FP _ -> 200
  | LOAD_F32_LE_FP _ -> 30
  | LOAD_F64_LE_FP _ -> 30
  | SIGMOID_FP _ -> 20
  | SOFTPLUS_FP _ -> 20
  | CAUSAL_DEPTHWISE_CONV1D_FP _ -> 100
  | GATED_DELTA_RULE_FP _ -> 200
  | SHIFT_ROUND_INPLACE _ -> 5
  | MATMUL_FP _ -> 200
  | RMSNORM_FP _ -> 50
  | RMSNORM_FP_EPS _ -> 50
  | L2NORM_FP _ -> 40
  | SILU_FP _ -> 20
  | ELEMWISE_MUL_FP _ -> 10
  | RESIDUAL_ADD_FP _ -> 10
  | ROPE_APPLY_FP _ -> 100
  | ROPE_APPLY_INDEXED_FP _ -> 100
  | LOAD_INT8_FP _ -> 30
  | VECDOT_FP _ -> 20
  | ARGMAX_FP _ -> 5
  | ATTENTION_SCORES_FP _ -> 100
  | SOFTMAX_FP _ -> 100
  | ATTENTION_WEIGHTED_SUM_FP _ -> 100
  | ATTENTION_KV_FP _ -> 200
  | ATTENTION_KV_Q16 _ -> 200
  | APPEND_VEC_FP _ -> 5

let is_valid_addr s =
  let len = String.length s in
  if len <> 47 then false
  else if not (String.length s >= 3 && String.sub s 0 3 = "oct") then false
  else
    let base58 = "123456789ABCDEFGHJKLMNPQRSTUVWXYZabcdefghijkmnopqrstuvwxyz" in
    let rec check i =
      if i >= len then true
      else if String.contains base58 (String.get s i) then check (i + 1)
      else false
    in check 3

let create_state ?(limit=1_000_000) ?(ctx=default_ctx) ?(depth=0) ?(is_view=false)
    ?(strict_values=false) ?(strict_blobs=false) ?(storage_kinds=[])
    ~caller ~origin ~address ~value ~storage () =
  {
    regs = Array.make 64 (VInt Z.zero);
    memory = { data = Hashtbl.create 1024; size = 0 };
    storage;
    effort_used = 0;
    effort_limit = limit;
    reverted = false;
    pc = 0;
    caller; origin; address; value;
    logs = ref [];
    ctx;
    undo_stack = [];
    undo_id = 0;
    call_depth = depth;
    return_stack = [];
    blobs = Hashtbl.create 16;
    is_view;
    strict_values;
    storage_kinds = Hashtbl.of_seq (List.to_seq storage_kinds);
    strict_blobs;
    decoded_chunk_cache = Hashtbl.create 512;
  }

let to_z = function
  | VInt z -> z
  | VU64 z -> z
  | VU128 z -> z
  | VU256 z -> z
  | VBool b -> if b then Z.one else Z.zero
  | VString s -> (try Z.of_string s with _ -> Z.zero)
  | _ -> Z.zero

let to_bool = function
  | VBool b -> b
  | VInt z -> not (Z.equal z Z.zero)
  | VString s -> s <> ""
  | _ -> false

let to_string = function
  | VString s -> s
  | VInt z -> Z.to_string z
  | VBool b -> if b then "true" else "false"
  | VBytes b -> b
  | VBytes32 b -> b
  | VU64 z -> Z.to_string z
  | VU128 z -> Z.to_string z
  | VU256 z -> Z.to_string z
  | VAddr a -> a
  | VCipher _ -> "<cipher>"
  | VPubKey _ -> "<pubkey>"

let to_cipher = function VCipher c -> Some c | _ -> None
let to_pubkey = function VPubKey pk -> Some pk | _ -> None

let max_u64 = Z.of_string "18446744073709551615"
let max_u128 = Z.sub (Z.shift_left Z.one 128) Z.one
let max_u256 = Z.sub (Z.shift_left Z.one 256) Z.one

let validate_u64 z =
  Z.sign z >= 0 && Z.compare z max_u64 <= 0

let validate_u128 z =
  Z.sign z >= 0 && Z.compare z max_u128 <= 0

let validate_u256 z =
  Z.sign z >= 0 && Z.compare z max_u256 <= 0

let validate_bytes32 s = String.length s = 32

let numeric_value = function
  | VInt _ | VU64 _ | VU128 _ | VU256 _ -> true
  | _ -> false

let storage_value_matches kind value =
  match kind, value with
  | StorageInt, (VInt _ | VU64 _ | VU128 _ | VU256 _)
  | StorageBool, VBool _
  | StorageString, VString _
  | StorageBytes, VBytes _
  | StorageBytes32, VBytes32 _
  | StorageAddr, VAddr _ -> true
  | StorageU64, value -> numeric_value value && validate_u64 (to_z value)
  | StorageU128, value -> numeric_value value && validate_u128 (to_z value)
  | StorageU256, value -> numeric_value value && validate_u256 (to_z value)
  | _ -> false

let storage_default = function
  | StorageInt -> VInt Z.zero
  | StorageBool -> VBool false
  | StorageString -> VString ""
  | StorageBytes -> VBytes ""
  | StorageBytes32 -> VBytes32 (String.make 32 '\x00')
  | StorageU64 -> VU64 Z.zero
  | StorageU128 -> VU128 Z.zero
  | StorageU256 -> VU256 Z.zero
  | StorageAddr -> VAddr ""

let storage_decode kind raw =
  try
    match kind with
    | StorageInt -> Some (VInt (Z.of_string raw))
    | StorageBool ->
      if String.equal raw "true" then Some (VBool true)
      else if String.equal raw "false" then Some (VBool false)
      else None
    | StorageString -> Some (VString raw)
    | StorageBytes -> Some (VBytes raw)
    | StorageBytes32 ->
      if validate_bytes32 raw then Some (VBytes32 raw) else None
    | StorageU64 ->
      let value = Z.of_string raw in
      if validate_u64 value then Some (VU64 value) else None
    | StorageU128 ->
      let value = Z.of_string raw in
      if validate_u128 value then Some (VU128 value) else None
    | StorageU256 ->
      let value = Z.of_string raw in
      if validate_u256 value then Some (VU256 value) else None
    | StorageAddr ->
      if is_valid_addr raw then Some (VAddr raw) else None
  with _ ->
    None

let load_storage_value st key =
  match Hashtbl.find_opt st.storage_kinds key with
  | None ->
    Some
      (match Hashtbl.find_opt st.storage key with
       | Some raw -> VString raw
       | None -> VString "0")
  | Some kind ->
    match Hashtbl.find_opt st.storage key with
    | None -> Some (storage_default kind)
    | Some raw -> storage_decode kind raw

let round_float_to_int f =
  if Float.is_nan f || Float.is_integer f then int_of_float f
  else if f >= 0.0 then int_of_float (f +. 0.5)
  else -int_of_float (-. f +. 0.5)

let fp64_to_z f =
  Z.of_int64 (Int64.bits_of_float f)

let z_to_fp64 z =
  if Z.fits_int64 z then Int64.float_of_bits (Z.to_int64 z) else 0.0

let mem_get_fp64 mem a =
  match Hashtbl.find_opt mem a with
  | Some (VInt z) -> z_to_fp64 z
  | _ -> 0.0

let finite_fp64 value =
  match classify_float value with
  | FP_nan
  | FP_infinite -> false
  | FP_normal
  | FP_subnormal
  | FP_zero -> true

let mem_read_fp64 mem a =
  match Hashtbl.find_opt mem a with
  | Some (VInt z) when Z.fits_int64 z ->
    let value = Int64.float_of_bits (Z.to_int64 z) in
    if finite_fp64 value then Some value else None
  | _ -> None

let mem_read_fp64_bits mem a =
  match Hashtbl.find_opt mem a with
  | Some (VInt z) when Z.fits_int64 z ->
    let bits = Z.to_int64 z in
    if Inference_fp64.finite bits then Some bits else None
  | _ -> None

let mem_set_fp64 mem a f =
  Hashtbl.replace mem a (VInt (fp64_to_z f))

let mem_set_fp64_bits mem a bits =
  Hashtbl.replace mem a (VInt (Z.of_int64 bits))

let f32_le_bits data offset =
  Char.code data.[offset]
  lor (Char.code data.[offset + 1] lsl 8)
  lor (Char.code data.[offset + 2] lsl 16)
  lor (Char.code data.[offset + 3] lsl 24)

(* Exact finite f32 → f64 bit-pattern widen (no host float dual-write). *)
let f32_le_to_fp64_bits data offset =
  let bits = f32_le_bits data offset in
  let sign32 = (bits lsr 31) land 1 in
  let exponent = (bits lsr 23) land 0xff in
  let fraction = bits land 0x7fffff in
  if exponent = 0xff then None
  else
    let sign64 = Int64.shift_left (Int64.of_int sign32) 63 in
    if exponent = 0 then
      if fraction = 0 then
        Some sign64
      else
        (* Subnormal f32 → normalized f64 (exact). *)
        let rec normalize frac exp =
          if frac land 0x800000 <> 0 then frac, exp
          else normalize (frac lsl 1) (exp - 1)
        in
        let frac_norm, exp_adj = normalize fraction 1 in
        let mantissa = (frac_norm land 0x7fffff) lsl 29 in
        (* unbiased = exp_adj - 127; f64_exp = unbiased + 1023 = exp_adj + 896 *)
        let exp64 = Int64.of_int (exp_adj + 896) in
        Some
          (Int64.logor
             sign64
             (Int64.logor
                (Int64.shift_left exp64 52)
                (Int64.of_int mantissa)))
    else
      (* Normal f32: f64_exp = exp - 127 + 1023 = exp + 896; frac << 29. *)
      let exp64 = Int64.of_int (exponent + 896) in
      let mantissa = Int64.shift_left (Int64.of_int fraction) 29 in
      Some
        (Int64.logor
           sign64
           (Int64.logor (Int64.shift_left exp64 52) mantissa))

let f32_le_to_fp64 data offset =
  Option.map Int64.float_of_bits (f32_le_to_fp64_bits data offset)

let f64_le_bits data offset =
  let bits = ref 0L in
  for i = 0 to 7 do
    bits :=
      Int64.logor
        !bits
        (Int64.shift_left
           (Int64.of_int (Char.code data.[offset + i]))
           (i * 8))
  done;
  !bits

let f64_le_to_fp64_bits data offset =
  let bits = f64_le_bits data offset in
  if Inference_fp64.finite bits then Some bits else None

let f64_le_to_fp64 data offset =
  Option.map Int64.float_of_bits (f64_le_to_fp64_bits data offset)

let fp16_le_to_fp64_bits data offset =
  let bits =
    Char.code data.[offset]
    lor (Char.code data.[offset + 1] lsl 8)
  in
  Inference_fp64.of_binary16 bits

let fp16_le_to_fp64 data offset =
  Option.map
    Int64.float_of_bits
    (fp16_le_to_fp64_bits data offset)

let make_u64 z =
  if validate_u64 z then Some (VU64 z) else None

let make_u128 z =
  if validate_u128 z then Some (VU128 z) else None

let make_u256 z =
  if validate_u256 z then Some (VU256 z) else None

let make_bytes32 s =
  if validate_bytes32 s then Some (VBytes32 s) else None
let to_bytes = function VBytes b -> Some b | VBytes32 b -> Some b | VString s -> Some s | _ -> None

let max_fhe_proof_bytes = Octra_core.Pvac_verify_policy.max_proof_raw_bytes
let max_fhe_cipher_bytes = 1_048_576
let max_fhe_pubkey_bytes = 18_000_000
let fhe_decode_bytes_per_effort = 16

let max_zk_vk_bytes = 131_072
let max_zk_proof_bytes = 1_024
let max_zk_inputs_bytes = 32_768
let fhe_verifier_lane = Mutex.create ()

let with_fhe_verifier_lane f =
  if not (Mutex.try_lock fhe_verifier_lane) then
    false
  else
    Fun.protect
      ~finally:(fun () -> Mutex.unlock fhe_verifier_lane)
      f

let encoded_worker_pubkey pk =
  Pvac_ffi.serialize_pubkey pk |> Bytes.to_string

let encoded_worker_cipher cipher =
  Octra_core.Crypto.FheBalance.encode_cipher cipher

let fhe_verifier_cipher_allowed cipher =
  try
    Octra_core.Pvac_verify_policy.shape_allowed
      (Pvac_ffi.cipher_shape cipher)
  with _ ->
    false

let proof_raw prefix prefix_len value =
  if
    String.length value > prefix_len
    && String.sub value 0 prefix_len = prefix
  then
    let encoded =
      String.sub value prefix_len (String.length value - prefix_len)
    in
    Base64.decode encoded
  else
    match Base64.decode value with
    | Ok raw -> Ok raw
    | Error _ -> Ok value

let worker_zero_proof value =
  let module FB = Octra_core.Crypto.FheBalance in
  match proof_raw FB.zero_proof_prefix FB.zero_proof_prefix_len value with
  | Error _ -> None
  | Ok raw ->
    Some (FB.zero_proof_prefix ^ Base64.encode_exn raw)

let worker_range_proof value =
  let module FB = Octra_core.Crypto.FheBalance in
  match proof_raw FB.range_proof_prefix FB.range_proof_prefix_len value with
  | Error _ -> None
  | Ok raw ->
    Some (FB.range_proof_prefix ^ Base64.encode_exn raw)

let worker_commitment value =
  if String.length value = 32 then
    Some (Base64.encode_exn value)
  else
    match Base64.decode value with
    | Ok raw when String.length raw = 32 ->
      Some (Base64.encode_exn raw)
    | Ok _
    | Error _ ->
      None

let ceil_div value divisor =
  if value <= 0 then 0 else 1 + ((value - 1) / divisor)

let decode_serialized_text s =
  match Base64.decode s with
  | Ok raw -> raw
  | Error _ -> s

let packed_pubkey_size raw =
  if String.length raw = 0 || Char.code raw.[0] <> 0xec then
    Some (String.length raw)
  else if String.length raw < 5 then
    None
  else
    let byte index = Char.code raw.[index] in
    let size =
      (byte 1 lsl 24)
      lor (byte 2 lsl 16)
      lor (byte 3 lsl 8)
      lor byte 4
    in
    if size > max_fhe_pubkey_bytes then None else Some size

let fhe_decode_input_cost encoded =
  ceil_div (String.length encoded) fhe_decode_bytes_per_effort

let fhe_pubkey_output_cost raw =
  match packed_pubkey_size raw with
  | None -> None
  | Some size ->
    Some (ceil_div size fhe_decode_bytes_per_effort)

let deser_bytes f s =
  match Base64.decode s with
  | Ok raw -> (try Some (f (Bytes.of_string raw)) with _ ->
    (try Some (f (Bytes.of_string s)) with _ -> None))
  | Error _ -> (try Some (f (Bytes.of_string s)) with _ -> None)

let decode_raw_or_b64_len expected s =
  if String.length s = expected then Some s
  else
    match Base64.decode s with
    | Ok raw when String.length raw = expected -> Some raw
    | _ -> None

let deterministic_seed parts =
  Bytes.of_string (Digestif.SHA256.(digest_string (String.concat "\000" parts) |> to_raw_string))

let revert st = st.reverted <- true; false

let revert_with_reason st reason =
  st.logs := { contract = st.address; depth = st.call_depth;
               event = "Require"; values = [VString reason] }
             :: !(st.logs);
  revert st

let view_guard st =
  if st.is_view then begin
    st.logs := { contract = st.address; depth = st.call_depth;
                 event = "Require"; values = [VString "write in view context"] }
               :: !(st.logs);
    ignore (revert st); false
  end else true

let getr st r = st.regs.(r)
let setr st r v = st.regs.(r) <- v

let is_numeric = numeric_value

let is_text = function
  | VString _ | VBytes _ | VBytes32 _ -> true
  | _ -> false

let is_address = function
  | VString _ | VAddr _ -> true
  | _ -> false

let is_scalar_value value =
  is_text value
  || is_address value
  || is_numeric value
  || match value with VBool _ -> true | _ -> false

let is_decimal = function
  | VString value ->
    (try
       ignore (Z.of_string value);
       true
     with _ ->
       false)
  | _ -> false

let is_add_operand left right =
  is_numeric left && is_numeric right
  || is_numeric left && Z.equal (to_z left) Z.zero && is_decimal right
  || is_decimal left && is_numeric right && Z.equal (to_z right) Z.zero

let comparable left right =
  match left, right with
  | VInt _, (VInt _ | VU64 _ | VU128 _ | VU256 _)
  | VU64 _, (VInt _ | VU64 _ | VU128 _ | VU256 _)
  | VU128 _, (VInt _ | VU64 _ | VU128 _ | VU256 _)
  | VU256 _, (VInt _ | VU64 _ | VU128 _ | VU256 _) -> true
  | VBool _, VBool _
  | VString _, VString _
  | VBytes _, VBytes _
  | VBytes32 _, VBytes32 _
  | VAddr _, VAddr _
  | VCipher _, VCipher _
  | VPubKey _, VPubKey _ -> true
  | _ -> false

let strict_operands st = function
  | ADD (_, a, b) ->
    is_add_operand (getr st a) (getr st b)
  | SUB (_, a, b) | MUL (_, a, b) | DIV (_, a, b) | MOD (_, a, b)
  | LT (_, a, b) | GT (_, a, b)
  | BITAND (_, a, b) | BITOR (_, a, b) | BITXOR (_, a, b)
  | BITSHL (_, a, b) | BITSHR (_, a, b) ->
    is_numeric (getr st a) && is_numeric (getr st b)
  | NEG (_, a) | ABS (_, a) | MLOADR (_, a) | MSTORER (a, _) ->
    is_numeric (getr st a)
  | VECDOT_Q16 (_, a, b, n) ->
    is_numeric (getr st a) && is_numeric (getr st b) && is_numeric (getr st n)
  | ELEMWISE_MUL_Q16 (a, b, n) | RESIDUAL_ADD_Q16 (a, b, n) ->
    is_numeric (getr st a) && is_numeric (getr st b) && is_numeric (getr st n)
  | EXP_Q16 (_, source) ->
    is_numeric (getr st source)
  | SOFTMAX_Q16_INPLACE (addr, count) ->
    is_numeric (getr st addr) && is_numeric (getr st count)
  | LAYERNORM_Q16_INPLACE (addr, count, gamma, beta) ->
    is_numeric (getr st addr) && is_numeric (getr st count) &&
    is_numeric (getr st gamma) && is_numeric (getr st beta)
  | RMSNORM_Q16_INPLACE (addr, count, gamma) ->
    is_numeric (getr st addr) && is_numeric (getr st count) &&
    is_numeric (getr st gamma)
  | SILU_Q16_INPLACE (addr, count) ->
    is_numeric (getr st addr) && is_numeric (getr st count)
  | ROPE_APPLY_Q16 (addr, count, position, base) ->
    is_numeric (getr st addr) && is_numeric (getr st count) &&
    is_numeric (getr st position) && is_numeric (getr st base)
  | ATTENTION_KV_Q16 (query, key, value, context, total, query_heads, key_heads, head_dim) ->
    List.for_all (fun reg -> is_numeric (getr st reg))
      [query; key; value; context; total; query_heads; key_heads; head_dim]
  | EQ (_, a, b) | NEQ (_, a, b) -> comparable (getr st a) (getr st b)
  | JIF (a, _) | ASSERT a ->
    (match getr st a with VBool _ -> true | _ -> false)
  | BALANCE (_, a) | SLOADK (_, a) | SDELK a | ISADDR (_, a)
  | ISHEX (_, a) | STATE_PATH_KEY (_, a) | ASSERT_ADDR a ->
    is_address (getr st a)
  | SSTORE (key, source) ->
    (match Hashtbl.find_opt st.storage_kinds key with
     | Some kind -> storage_value_matches kind (getr st source)
     | None -> is_scalar_value (getr st source))
  | SSTOREK (key, value) ->
    is_address (getr st key) && is_scalar_value (getr st value)
  | PARSE_INTS (_, text, base) ->
    is_text (getr st text) && is_numeric (getr st base)
  | OBJECT_MEMBER_COUNT (_, object_ref) -> is_text (getr st object_ref)
  | OBJECT_HAS_MEMBER (_, object_ref, member_ref) ->
    is_text (getr st object_ref) && is_text (getr st member_ref)
  | OBJECT_MEMBER_REF_AT (_, object_ref, index) ->
    is_text (getr st object_ref) && is_numeric (getr st index)
  | OBJECT_TRANSITION_APPLY
      (_, transition_ref, object_ref, previous_ref, next_ref, member_bundle,
       touched_hash, proof_kind, proof_hash, status, intent_id) ->
    List.for_all (fun reg -> is_text (getr st reg))
      [transition_ref; object_ref; previous_ref; next_ref; member_bundle;
       touched_hash; proof_kind; proof_hash; status; intent_id]
  | SUBSTR (_, text, start, length) ->
    is_text (getr st text) && is_numeric (getr st start) && is_numeric (getr st length)
  | INDEXOF (_, text, needle) ->
    is_text (getr st text) && is_text (getr st needle)
  | SHA256 (_, text) | KECCAK256 (_, text) | FSTORE (_, text)
  | FLOAD (_, text) -> is_text (getr st text)
  | SKEYS (_, prefix, base) ->
    is_text (getr st prefix) && is_numeric (getr st base)
  | SKEYS_PAGE (_, _, prefix, after, base) ->
    is_text (getr st prefix) && is_text (getr st after) && is_numeric (getr st base)
  | SLOADN (base_key, base_value, count)
  | SSTOREN (base_key, base_value, count) ->
    List.for_all (fun reg -> is_numeric (getr st reg)) [base_key; base_value; count]
  | CONCAT (_, left, right) ->
    is_scalar_value (getr st left) && is_scalar_value (getr st right)
  | STRLEN (_, text) -> is_text (getr st text)
  | XCALL (_, target, method_name, _, _) ->
    is_address (getr st target) && is_text (getr st method_name)
  | SPAWN (_, code) | SPAWN2 (_, code, _, _) -> is_text (getr st code)
  | TRANSFER (_, target, value) ->
    is_address (getr st target) && is_numeric (getr st value)
  | FHE_LOAD_PK (_, address) -> is_address (getr st address)
  | FHE_ADD (_, key, left, right) | FHE_SUB (_, key, left, right)
  | FHE_MUL (_, key, left, right) ->
    (match getr st key, getr st left, getr st right with
     | VPubKey _, VCipher _, VCipher _ -> true
     | _ -> false)
  | FHE_SCALE (_, key, cipher, scalar)
  | FHE_DIV_CONST (_, key, cipher, scalar)
  | FHE_ADD_CONST (_, key, cipher, scalar)
  | FHE_SUB_CONST (_, key, cipher, scalar) ->
    (match getr st key, getr st cipher with
     | VPubKey _, VCipher _ -> is_numeric (getr st scalar)
     | _ -> false)
  | FHE_VERIFY_ZERO (_, key, cipher, proof)
  | FHE_VERIFY_RANGE (_, key, cipher, proof) ->
    (match getr st key, getr st cipher with
     | VPubKey _, VCipher _ -> is_text (getr st proof)
     | _ -> false)
  | FHE_VERIFY_BOUND (_, key, cipher, proof, commitment) ->
    (match getr st key, getr st cipher with
     | VPubKey _, VCipher _ -> is_text (getr st proof) && is_text (getr st commitment)
     | _ -> false)
  | FHE_COMMIT (_, key, cipher) ->
    (match getr st key, getr st cipher with VPubKey _, VCipher _ -> true | _ -> false)
  | FHE_PEDERSEN (_, amount, blinding) ->
    is_numeric (getr st amount) && is_text (getr st blinding)
  | FHE_SER (_, cipher) -> (match getr st cipher with VCipher _ -> true | _ -> false)
  | FHE_DESER (_, bytes) | FHE_DESER_PK (_, bytes) -> is_text (getr st bytes)
  | FHE_SER_PK (_, key) -> (match getr st key with VPubKey _ -> true | _ -> false)
  | GROTH16_VERIFY_BN254 (_, key, proof, inputs) ->
    List.for_all (fun reg -> is_text (getr st reg)) [key; proof; inputs]
  | ED25519_OK (_, key, message, signature) ->
    List.for_all (fun reg -> is_text (getr st reg)) [key; message; signature]
  | LOAD_INT8_BYTES_TO_MEM (dst, source, offset, length, scale)
  | LOAD_INT8_B64_TO_MEM (dst, source, offset, length, scale) ->
    is_numeric (getr st dst) && is_text (getr st source)
    && is_numeric (getr st offset) && is_numeric (getr st length)
    && is_numeric (getr st scale)
  | LOAD_INT8_Q16 (dst, source, offset, length, scale) ->
    is_numeric (getr st dst) && is_text (getr st source)
    && is_numeric (getr st offset) && is_numeric (getr st length)
    && is_numeric (getr st scale)
  | LINEAR_Q1_G128_FP (dst, lhs, q1, offset, rows, inner, cols) ->
    is_numeric (getr st dst) && is_numeric (getr st lhs)
    && is_text (getr st q1) && is_numeric (getr st offset)
    && is_numeric (getr st rows) && is_numeric (getr st inner)
    && is_numeric (getr st cols)
  | LOAD_F32_LE_FP (dst, source, offset, length) ->
    is_numeric (getr st dst) && is_text (getr st source)
    && is_numeric (getr st offset) && is_numeric (getr st length)
  | LOAD_F64_LE_FP (dst, source, offset, length) ->
    is_numeric (getr st dst) && is_text (getr st source)
    && is_numeric (getr st offset) && is_numeric (getr st length)
  | ROPE_APPLY_INDEXED_FP
      (addr, count, head_dim, rot_dim, positions, base) ->
    List.for_all
      (fun reg -> is_numeric (getr st reg))
      [addr; count; head_dim; rot_dim; positions; base]
  | SIGMOID_FP (addr, count)
  | SOFTPLUS_FP (addr, count)
  | SILU_FP (addr, count) ->
    is_numeric (getr st addr) && is_numeric (getr st count)
  | RMSNORM_FP_EPS (addr, count, gamma, epsilon) ->
    is_numeric (getr st addr)
    && is_numeric (getr st count)
    && is_numeric (getr st gamma)
    && (match getr st epsilon with VInt z -> Z.fits_int64 z | _ -> false)
  | L2NORM_FP (addr, count, epsilon) ->
    is_numeric (getr st addr)
    && is_numeric (getr st count)
    && (match getr st epsilon with VInt z -> Z.fits_int64 z | _ -> false)
  | ELEMWISE_MUL_FP (dst, source, count)
  | RESIDUAL_ADD_FP (dst, source, count) ->
    List.for_all (fun reg -> is_numeric (getr st reg)) [dst; source; count]
  | CAUSAL_DEPTHWISE_CONV1D_FP (dst, input, kernel, timesteps, channels, width) ->
    List.for_all (fun reg -> is_numeric (getr st reg))
      [dst; input; kernel; timesteps; channels; width]
  | GATED_DELTA_RULE_FP
      (output, state_dst, q, k, v, log_decay, beta, state, timesteps,
       q_heads, k_heads, v_heads, key_dim, value_dim) ->
    List.for_all
      (fun reg -> is_numeric (getr st reg))
      [output; state_dst; q; k; v; log_decay; beta; state; timesteps;
       q_heads; k_heads; v_heads; key_dim; value_dim]
  | APPEND_VEC_Q16 (dst, pos, source, length) ->
    List.for_all (fun reg -> is_numeric (getr st reg)) [dst; pos; source; length]
  | ARGMAX_Q16 (dest, addr, length)
  | ARGMAX_FP (dest, addr, length) ->
    is_numeric (getr st dest) && is_numeric (getr st addr) && is_numeric (getr st length)
  | ATTENTION_SCORES_FP (dest, query, key, key_count, head_dim) ->
    List.for_all
      (fun reg -> is_numeric (getr st reg))
      [dest; query; key; key_count; head_dim]
  | SOFTMAX_FP (dest, scores, count) ->
    List.for_all
      (fun reg -> is_numeric (getr st reg))
      [dest; scores; count]
  | ATTENTION_WEIGHTED_SUM_FP (dest, probs, value, key_count, head_dim) ->
    List.for_all
      (fun reg -> is_numeric (getr st reg))
      [dest; probs; value; key_count; head_dim]
  | _ -> true

let strict_ok st op = not st.strict_values || strict_operands st op

let read_int st reg =
  let value = to_z (getr st reg) in
  if Z.fits_int value then Some (Z.to_int value) else None

let checked_product left right =
  if left < 0 || right < 0 then None
  else if left = 0 || right = 0 then Some 0
  else if left > max_int / right then None
  else Some (left * right)

let checked_product_many factors =
  let rec loop acc = function
    | [] -> Some acc
    | factor :: rest ->
      (match checked_product acc factor with
       | None -> None
       | Some acc -> loop acc rest)
  in
  loop 1 factors

let valid_mem_span_with_limit max_cells addr n =
  addr >= 0 && n > 0 && n <= max_cells && addr <= max_int - n

let valid_mem_span addr n =
  valid_mem_span_with_limit 131072 addr n

let valid_large_mem_span addr n =
  valid_mem_span_with_limit 1_048_576 addr n

let ranges_overlap left left_n right right_n =
  left < right + right_n && right < left + left_n

let same_range left left_n right right_n =
  left = right && left_n = right_n

let read_fp64_array mem addr n =
  let values = Array.init n (fun i -> mem_read_fp64 mem (addr + i)) in
  if Array.for_all Option.is_some values then Some (Array.map Option.get values)
  else None

let read_fp64_bits_array mem addr n =
  let values = Array.init n (fun i -> mem_read_fp64_bits mem (addr + i)) in
  if Array.for_all Option.is_some values then Some (Array.map Option.get values)
  else None

let decode_q1_g128_scale_bits q1 off blocks =
  let block_bytes = 18 in
  let scales = Array.make blocks 0L in
  let ok = ref true in
  for block = 0 to blocks - 1 do
    let block_offset = off + (block * block_bytes) in
    match fp16_le_to_fp64_bits q1 block_offset with
    | None -> ok := false
    | Some scale -> scales.(block) <- scale
  done;
  if !ok then Some scales else None

let max_exact_fp64_int =
  Z.of_int64 9_007_199_254_740_991L

let read_position_array mem addr n =
  let values =
    Array.init n (fun i ->
      match Hashtbl.find_opt mem (addr + i) with
      | Some (VInt z)
        when Z.fits_int64 z && Z.leq (Z.abs z) max_exact_fp64_int ->
        Some (Z.to_int64 z)
      | _ -> None)
  in
  if Array.for_all Option.is_some values then Some (Array.map Option.get values)
  else None

let read_fp64_reg st reg =
  match getr st reg with
  | VInt z when Z.fits_int64 z ->
    let value = Int64.float_of_bits (Z.to_int64 z) in
    if finite_fp64 value then Some value else None
  | _ -> None

let read_fp64_reg_bits st reg =
  match getr st reg with
  | VInt z when Z.fits_int64 z ->
    let bits = Z.to_int64 z in
    if Inference_fp64.finite bits then Some bits else None
  | _ -> None

let fp64_one_bits = Int64.bits_of_float 1.0

let fp64_positive_bits =
  Inference_fp64.positive

let fp64_inverse_sqrt_bits =
  Inference_fp64.inverse_sqrt

let fp64_sigmoid_bits bits =
  match Inference_fp64.compare bits 0L with
  | Some cmp when cmp >= 0 ->
    (* sigmoid(x) = 1 / (1 + exp(-x)) for x >= 0; exp(-x) is protocol-owned. *)
    (match Inference_fp64.exp_nonpositive (Inference_fp64.negate bits) with
     | Some exp_bits ->
       (match Inference_fp64.add fp64_one_bits exp_bits with
        | Some denom -> Inference_fp64.div fp64_one_bits denom
        | None -> None)
     | None -> None)
  | Some _ ->
    (* sigmoid(x) = exp(x) / (1 + exp(x)) for x < 0; exp(x) is protocol-owned. *)
    (match Inference_fp64.exp_nonpositive bits with
     | Some exp_bits ->
       (match Inference_fp64.add fp64_one_bits exp_bits with
        | Some denom -> Inference_fp64.div exp_bits denom
        | None -> None)
     | None -> None)
  | None -> None

let fp64_silu_bits bits =
  match fp64_sigmoid_bits bits with
  | Some sigmoid -> Inference_fp64.mul bits sigmoid
  | None -> None

let fp64_softplus_bits bits =
  match Inference_fp64.compare bits 0L with
  | Some cmp when cmp > 0 ->
    (* softplus(x) = x + log1p(exp(-x)) for x > 0; both halves protocol-owned. *)
    (match Inference_fp64.exp_nonpositive (Inference_fp64.negate bits) with
     | Some exp_bits ->
       (match Inference_fp64.log1p_nonnegative exp_bits with
        | Some tail_bits -> Inference_fp64.add bits tail_bits
        | None -> None)
     | None -> None)
  | Some _ ->
    (* softplus(x) = log1p(exp(x)) for x <= 0. *)
    (match Inference_fp64.exp_nonpositive bits with
     | Some exp_bits -> Inference_fp64.log1p_nonnegative exp_bits
     | None -> None)
  | None -> None

let gated_delta_rule_effort timesteps v_heads value_dim key_dim =
  let scale_product factors scale =
    match Cost.product factors with
    | None -> None
    | Some cost -> Cost.product [cost; scale]
  in
  match
    scale_product [timesteps; v_heads; value_dim; key_dim] 4,
    scale_product [timesteps; v_heads; value_dim] 2,
    Cost.product [timesteps; v_heads]
  with
  | Some inner, Some rows, Some heads ->
    (match Cost.add inner rows with
     | None -> None
     | Some subtotal -> Cost.add subtotal heads)
  | _ -> None

let read_q16 st addr n =
  if not (valid_mem_span addr n) then None
  else
    let values = Array.init n (fun i ->
      match Hashtbl.find_opt st.memory.data (addr + i) with
      | None -> Some Z.zero
      | Some value when is_numeric value && Fixed_q16.in_range (to_z value) ->
        Some (to_z value)
      | Some _ -> None)
    in
    if Array.for_all Option.is_some values then
      Some (Array.map Option.get values)
    else None

let write_q16 st addr values =
  Array.iteri (fun i value ->
    let cell = addr + i in
    Hashtbl.replace st.memory.data cell (VInt value);
    if cell >= st.memory.size then st.memory.size <- cell + 1) values

let valid_reg_span base count =
  count >= 0 && base >= 0 && base <= 63 && count <= 64 - base

let add_dyn_effort st cost =
  match Cost.charge ~used:st.effort_used ~cost ~limit:st.effort_limit with
  | None -> false
  | Some effort -> st.effort_used <- effort; true

let add_dyn_product st factors divisor =
  match Cost.scaled_product factors ~divisor with
  | None -> false
  | Some cost -> add_dyn_effort st cost

let map_fp64_inplace st addr n f =
  if n <= 0 || n > 131072 || not (valid_mem_span addr n) then revert st
  else if not (add_dyn_product st [n; 3] 1) then revert st
  else
    let input = Array.init n (fun i -> mem_read_fp64 st.memory.data (addr + i)) in
    if not (Array.for_all Option.is_some input) then revert st
    else
      let output = Array.map (fun value -> f (Option.get value)) input in
      if not (Array.for_all finite_fp64 output) then revert st
      else begin
        for i = 0 to n - 1 do
          mem_set_fp64 st.memory.data (addr + i) output.(i)
        done;
        true
      end

let map_fp64_bits_inplace st addr n f =
  if n <= 0 || n > 131072 || not (valid_mem_span addr n) then revert st
  else if not (add_dyn_product st [n; 3] 1) then revert st
  else
    match read_fp64_bits_array st.memory.data addr n with
    | None -> revert st
    | Some input ->
      let output = Array.map f input in
      if
        not
          (Array.for_all
             (function Some bits -> Inference_fp64.finite bits | None -> false)
             output)
      then revert st
      else begin
        for i = 0 to n - 1 do
          mem_set_fp64_bits st.memory.data (addr + i) (Option.get output.(i))
        done;
        true
      end

let object_apply_dyn_cost writes =
  List.fold_left
    (fun acc write ->
      let cost = match write with
      | Octra_core.Circle_object_apply.Set (_key, value) ->
        10 + (String.length value / 32)
      | Octra_core.Circle_object_apply.Del _ ->
        5
      in
      Option.bind acc (fun total -> Cost.add total cost))
    (Some 0)
    writes

let apply_object_write st = function
  | Octra_core.Circle_object_apply.Set (key, value) ->
    let old_val = Hashtbl.find_opt st.storage key in
    st.undo_stack <- UndoWrite (key, old_val) :: st.undo_stack;
    Hashtbl.replace st.storage key value;
    true
  | Octra_core.Circle_object_apply.Del key ->
    let old_val = Hashtbl.find_opt st.storage key in
    st.undo_stack <- UndoWrite (key, old_val) :: st.undo_stack;
    Hashtbl.remove st.storage key;
    true

let rec apply_object_writes st = function
  | [] ->
    true
  | write :: rest ->
    if apply_object_write st write then
      apply_object_writes st rest
    else
      false

let exec_one st op =
  if not (add_dyn_effort st (effort_cost op)) then revert st
  else if not (strict_ok st op) then revert st
  else match op with
  | ADD (rd, rs1, rs2) ->
    setr st rd (VInt (Z.add (to_z (getr st rs1)) (to_z (getr st rs2)))); true
  | SUB (rd, rs1, rs2) ->
    setr st rd (VInt (Z.sub (to_z (getr st rs1)) (to_z (getr st rs2)))); true
  | MUL (rd, rs1, rs2) ->
    setr st rd (VInt (Z.mul (to_z (getr st rs1)) (to_z (getr st rs2)))); true
  | DIV (rd, rs1, rs2) ->
    let d = to_z (getr st rs2) in
    if Z.equal d Z.zero then revert st
    else (setr st rd (VInt (Z.div (to_z (getr st rs1)) d)); true)
  | MOD (rd, rs1, rs2) ->
    let d = to_z (getr st rs2) in
    if Z.equal d Z.zero then revert st
    else (setr st rd (VInt (Z.rem (to_z (getr st rs1)) d)); true)
  | NEG (rd, rs) ->
    setr st rd (VInt (Z.neg (to_z (getr st rs)))); true
  | ABS (rd, rs) ->
    setr st rd (VInt (Z.abs (to_z (getr st rs)))); true
  | EQ (rd, rs1, rs2) ->
    let a = getr st rs1 and b = getr st rs2 in
    (match a, b with

     | (VCipher _ | VPubKey _), VInt z when Z.equal z Z.zero ->
       setr st rd (VBool false); true
     | VInt z, (VCipher _ | VPubKey _) when Z.equal z Z.zero ->
       setr st rd (VBool false); true

     | VCipher a, VCipher b -> setr st rd (VBool (a == b)); true
     | VPubKey a, VPubKey b -> setr st rd (VBool (a == b)); true

     | VCipher _, _ | _, VCipher _ | VPubKey _, _ | _, VPubKey _ ->
       setr st rd (VBool false); true
     | _ -> setr st rd (VBool (to_string a = to_string b)); true)
  | LT (rd, rs1, rs2) ->
    let a = getr st rs1 and b = getr st rs2 in
    (match a, b with
     | VCipher _, _ | _, VCipher _ | VPubKey _, _ | _, VPubKey _ ->
       setr st rd (VBool false); true
     | _ -> setr st rd (VBool (Z.lt (to_z a) (to_z b))); true)
  | GT (rd, rs1, rs2) ->
    let a = getr st rs1 and b = getr st rs2 in
    (match a, b with
     | VCipher _, _ | _, VCipher _ | VPubKey _, _ | _, VPubKey _ ->
       setr st rd (VBool false); true
     | _ -> setr st rd (VBool (Z.gt (to_z a) (to_z b))); true)
  | NEQ (rd, rs1, rs2) ->
    let a = getr st rs1 and b = getr st rs2 in
    (match a, b with
     | (VCipher _ | VPubKey _), VInt z when Z.equal z Z.zero ->
       setr st rd (VBool true); true
     | VInt z, (VCipher _ | VPubKey _) when Z.equal z Z.zero ->
       setr st rd (VBool true); true
     | VCipher a, VCipher b -> setr st rd (VBool (a != b)); true
     | VPubKey a, VPubKey b -> setr st rd (VBool (a != b)); true
     | VCipher _, _ | _, VCipher _ | VPubKey _, _ | _, VPubKey _ ->
       setr st rd (VBool true); true
     | _ -> setr st rd (VBool (to_string a <> to_string b)); true)
  | LDI (rd, v) ->
    setr st rd v; true
  | MOV (rd, rs) ->
    setr st rd (getr st rs); true
  | SLOAD (rd, key) ->
    (match load_storage_value st key with
     | Some value -> setr st rd value; true
     | None -> revert st)
  | SSTORE (key, rs) ->
    if not (view_guard st) then false
    else if is_reserved_key key then revert st
    else
    (match getr st rs with
     | VCipher _ | VPubKey _ -> revert st
     | v ->
       let s = to_string v in
       let len = String.length s in
       if len > max_storage_value_len then revert st
       else begin
         if not (add_dyn_effort st (len / 32)) then revert st
         else begin
           let old_val = Hashtbl.find_opt st.storage key in
           st.undo_stack <- UndoWrite (key, old_val) :: st.undo_stack;
           Hashtbl.replace st.storage key s; true
         end
       end)
  | SDEL key ->
    if not (view_guard st) then false
    else if is_reserved_key key then revert st
    else begin
      let old_val = Hashtbl.find_opt st.storage key in
      st.undo_stack <- UndoWrite (key, old_val) :: st.undo_stack;
      Hashtbl.remove st.storage key; true
    end
  | SDELK rk ->
    if not (view_guard st) then false
    else
      let key = to_string (getr st rk) in
      if is_reserved_key key then revert st
      else begin
        let old_val = Hashtbl.find_opt st.storage key in
        st.undo_stack <- UndoWrite (key, old_val) :: st.undo_stack;
        Hashtbl.remove st.storage key; true
      end
  | SLOADK (rd, rs) ->
    let key = to_string (getr st rs) in
    let v = match Hashtbl.find_opt st.storage key with
      | Some s -> VString s | None -> VString "0" in
    setr st rd v; true
  | SSTOREK (rk, rv) ->
    if not (view_guard st) then false
    else
    (match getr st rv with
     | VCipher _ | VPubKey _ -> revert st
     | v ->
       let key = to_string (getr st rk) in
       if is_reserved_key key then revert st
       else
       let s = to_string v in
       let len = String.length s in
       if len > max_storage_value_len then revert st
       else begin
         if not (add_dyn_effort st (len / 32)) then revert st
         else begin
           let old_val = Hashtbl.find_opt st.storage key in
           st.undo_stack <- UndoWrite (key, old_val) :: st.undo_stack;
           Hashtbl.replace st.storage key s; true
         end
       end)
  | MLOAD (rd, idx) ->
    (match Hashtbl.find_opt st.memory.data idx with
     | Some value -> setr st rd value; true
     | None when st.strict_values -> revert st
     | None -> setr st rd (VInt Z.zero); true)
  | MSTORE (idx, rs) ->
    Hashtbl.replace st.memory.data idx (getr st rs);
    if idx >= st.memory.size then st.memory.size <- idx + 1;
    true
  | MLOADR (rd, rs_idx) ->
    let idx = Z.to_int (to_z (getr st rs_idx)) in
    if idx < 0 || idx > 16_777_216 then revert st
    else begin
      (match Hashtbl.find_opt st.memory.data idx with
       | Some value -> setr st rd value; true
       | None when st.strict_values -> revert st
       | None -> setr st rd (VInt Z.zero); true)
    end
  | MSTORER (rs_idx, rs_val) ->
    let idx = Z.to_int (to_z (getr st rs_idx)) in
    if idx < 0 || idx > 16_777_216 then revert st
    else begin
      Hashtbl.replace st.memory.data idx (getr st rs_val);
      if idx >= st.memory.size then st.memory.size <- idx + 1;
      true
    end
  | PARSE_INTS (rd_count, rs_string, rs_base) ->
    let s = to_string (getr st rs_string) in
    let base = Z.to_int (to_z (getr st rs_base)) in
    if base < 0 || base > 16_777_216 then revert st
    else begin
      let parts = String.split_on_char ',' s in
      let count = ref 0 in
      let ok = ref true in
      List.iter (fun part ->
        if !ok then begin
          let trimmed = String.trim part in
          if trimmed <> "" then begin
            let idx = base + !count in
            if idx > 16_777_216 then ok := false
            else begin
              (try
                Hashtbl.replace st.memory.data idx (VInt (Z.of_string trimmed))
              with _ ->
                if st.strict_values then ok := false
                else Hashtbl.replace st.memory.data idx (VInt Z.zero));
              if idx >= st.memory.size then st.memory.size <- idx + 1;
              if not (add_dyn_effort st 2) then ok := false
              else count := !count + 1
            end
          end
        end
      ) parts;
      if not !ok then revert st
      else begin
        setr st rd_count (VInt (Z.of_int !count)); true
      end
    end
  | ISADDR (rd, rs) ->
    setr st rd (VBool (is_valid_addr (to_string (getr st rs)))); true
  | ISHEX (rd, rs) ->
    let s = to_string (getr st rs) in
    let is_hex = String.length s > 0 && try
      String.iter (fun c -> match c with
        | '0'..'9' | 'a'..'f' | 'A'..'F' -> ()
        | _ -> raise Exit
      ) s; true
    with Exit -> false in
    setr st rd (VBool is_hex); true
  | STATE_PATH_KEY (rd, rs) ->
    begin
      match Octra_core.Circles.path_key_of_state_ref (to_string (getr st rs)) with
      | Ok (_, _, path_key) ->
        setr st rd (VString path_key);
        true
      | Error _ ->
        revert st
    end
  | OBJECT_MEMBER_COUNT (rd, robject_ref) ->
    let object_ref = to_string (getr st robject_ref) in
    let count =
      Octra_core.Circle_object_member_query.member_count_in_storage_tbl
        st.storage
        object_ref in
    setr st rd (VInt (Z.of_int count));
    true
  | OBJECT_HAS_MEMBER (rd, robject_ref, rmember_ref) ->
    let object_ref = to_string (getr st robject_ref) in
    let member_ref = to_string (getr st rmember_ref) in
    let present =
      Octra_core.Circle_object_member_query.has_member_in_storage_tbl
        st.storage
        object_ref
        member_ref in
    setr st rd (VBool present);
    true
  | OBJECT_MEMBER_REF_AT (rd, robject_ref, rindex) ->
    let object_ref = to_string (getr st robject_ref) in
    let index = Z.to_int (to_z (getr st rindex)) in
    begin
      match
        Octra_core.Circle_object_member_query.member_ref_at_in_storage_tbl
          st.storage
          object_ref
          index
      with
      | Some member_ref ->
        setr st rd (VString member_ref);
        true
      | None ->
        setr st rd (VString "");
        true
    end
  | OBJECT_TRANSITION_APPLY
      ( rd,
        rtransition_ref,
        robject_ref,
        rprevious_state_ref,
        rnext_state_ref,
        rmember_bundle,
        rtouched_members_hash,
        rproof_kind,
        rproof_receipt_hash,
        rstatus,
        rintent_id ) ->
    if not (view_guard st) then false
    else
      begin
        match
          Octra_core.Circle_object_apply.apply
            ~current_epoch:st.ctx.current_epoch
            ~storage_tbl:st.storage
            ~transition_ref:(to_string (getr st rtransition_ref))
            ~object_ref:(to_string (getr st robject_ref))
            ~previous_state_ref:(to_string (getr st rprevious_state_ref))
            ~next_state_ref:(to_string (getr st rnext_state_ref))
            ~member_bundle:(to_string (getr st rmember_bundle))
            ~touched_members_hash:(to_string (getr st rtouched_members_hash))
            ~proof_kind_raw:(to_string (getr st rproof_kind))
            ~proof_receipt_hash_raw:(to_string (getr st rproof_receipt_hash))
            ~status:(to_string (getr st rstatus))
            ~intent_id:(to_string (getr st rintent_id))
        with
        | Error _ ->
          revert st
        | Ok result ->
          match object_apply_dyn_cost result.writes with
          | None ->
            revert st
          | Some cost ->
          if not (add_dyn_effort st cost) then
            revert st
          else if
            List.exists
              (function
                | Octra_core.Circle_object_apply.Set (_key, value) ->
                  String.length value > max_storage_value_len
                | Octra_core.Circle_object_apply.Del _ ->
                  false)
              result.writes
          then
            revert st
          else if apply_object_writes st result.writes then begin
            setr st rd (VInt (Z.of_int64 result.version));
            true
          end else
            revert st
      end
  | ASSERT_ADDR rs ->
    let s = to_string (getr st rs) in
    if is_valid_addr s then true
    else begin
      st.logs := { contract = st.address; depth = st.call_depth;
                   event = "Require"; values = [VString "invalid address"] }
                 :: !(st.logs);
      revert st
    end
  | SUBSTR (rd, rs, rstart, rlen) ->
    let s = to_string (getr st rs) in
    let slen = String.length s in
    if not (add_dyn_effort st (slen / 256)) then revert st
    else
    let start = Z.to_int (to_z (getr st rstart)) in
    let len = Z.to_int (to_z (getr st rlen)) in
    if start < 0 || start > slen || len < 0 then setr st rd (VString "")
    else begin
      let actual_len = min len (slen - start) in
      setr st rd (VString (String.sub s start actual_len))
    end; true
  | INDEXOF (rd, rs, rsearch) ->
    let s = to_string (getr st rs) in
    let search = to_string (getr st rsearch) in
    let slen = String.length s in
    let plen = String.length search in
    if not (add_dyn_product st [slen; max plen 1] 1024) then revert st
    else if plen = 0 then (setr st rd (VInt Z.zero); true)
    else if plen > slen then (setr st rd (VInt (Z.of_int (-1))); true)
    else begin
      let found = ref (-1) in
      for i = 0 to slen - plen do
        if !found = -1 && String.sub s i plen = search then found := i
      done;
      setr st rd (VInt (Z.of_int !found)); true
    end
  | SHA256 (rd, rs) ->
    let s = to_string (getr st rs) in
    if not (add_dyn_effort st ((String.length s) / 64)) then revert st
    else begin
      let h = Digestif.SHA256.digest_string s in
      setr st rd (VString (Digestif.SHA256.to_hex h)); true
    end
  | KECCAK256 (rd, rs) ->
    let s = to_string (getr st rs) in
    if not (add_dyn_effort st ((String.length s) / 64)) then revert st
    else begin
      let h = Digestif.KECCAK_256.digest_string s in
      setr st rd (VString (Digestif.KECCAK_256.to_hex h)); true
    end
  | ED25519_OK (rd, rpk, rmsg, rsig) ->
    (match to_bytes (getr st rpk), to_bytes (getr st rmsg), to_bytes (getr st rsig) with
     | Some pk_in, Some msg, Some sig_in ->
       if not (add_dyn_effort st ((String.length msg) / 64)) then revert st
       else
         let ok =
           match decode_raw_or_b64_len 32 pk_in, decode_raw_or_b64_len 64 sig_in with
           | Some pk_raw, Some sig_raw ->
             let pk_b64 = Base64.encode_exn pk_raw in
             let sig_b64 = Base64.encode_exn sig_raw in
             Octra_core.Peer_auth.verify msg sig_b64 pk_b64
           | _ -> false
         in
         setr st rd (VBool ok); true
     | _ -> revert st)
  | BITAND (rd, ra, rb) ->
    let mask64 = Z.sub (Z.shift_left Z.one 64) Z.one in
    let a = Z.logand (to_z (getr st ra)) mask64 in
    let b = Z.logand (to_z (getr st rb)) mask64 in
    setr st rd (VInt (Z.logand a b)); true
  | BITOR (rd, ra, rb) ->
    let mask64 = Z.sub (Z.shift_left Z.one 64) Z.one in
    let a = Z.logand (to_z (getr st ra)) mask64 in
    let b = Z.logand (to_z (getr st rb)) mask64 in
    setr st rd (VInt (Z.logor a b)); true
  | BITXOR (rd, ra, rb) ->
    let mask64 = Z.sub (Z.shift_left Z.one 64) Z.one in
    let a = Z.logand (to_z (getr st ra)) mask64 in
    let b = Z.logand (to_z (getr st rb)) mask64 in
    setr st rd (VInt (Z.logxor a b)); true
  | BITSHL (rd, ra, rb) ->
    let mask64 = Z.sub (Z.shift_left Z.one 64) Z.one in
    let a = Z.logand (to_z (getr st ra)) mask64 in
    let n = Z.to_int (to_z (getr st rb)) in
    if n < 0 || n > 63 then (setr st rd (VInt Z.zero); true)
    else (setr st rd (VInt (Z.logand (Z.shift_left a n) mask64)); true)
  | BITSHR (rd, ra, rb) ->
    let mask64 = Z.sub (Z.shift_left Z.one 64) Z.one in
    let a = Z.logand (to_z (getr st ra)) mask64 in
    let n = Z.to_int (to_z (getr st rb)) in
    if n < 0 || n > 63 then (setr st rd (VInt Z.zero); true)
    else (setr st rd (VInt (Z.shift_right a n)); true)
  | SKEYS (rd_count, rs_prefix, rs_base) ->
    let prefix = to_string (getr st rs_prefix) in
    let base = Z.to_int (to_z (getr st rs_base)) in
    let plen = String.length prefix in
    let max_keys = 1000 in

    let suffixes = ref [] in
    Hashtbl.iter (fun k _v ->
      if not (is_reserved_key k)
         && String.length k >= plen && String.sub k 0 plen = prefix then
        suffixes := String.sub k plen (String.length k - plen) :: !suffixes
    ) st.storage;
    let sorted = List.sort String.compare !suffixes in
    let count = ref 0 in
    let ok = ref true in
    List.iter (fun suffix ->
      if !ok && !count < max_keys then begin
        let idx = base + !count in
        if idx >= 0 && idx < 16_777_216 then begin
          if not (add_dyn_effort st 5) then ok := false
          else begin
            Hashtbl.replace st.memory.data idx (VString suffix);
            if idx >= st.memory.size then st.memory.size <- idx + 1;
            count := !count + 1
          end
        end
      end
    ) sorted;
    if not !ok then revert st
    else (setr st rd_count (VInt (Z.of_int !count)); true)
  | SKEYS_PAGE (rd_count, rd_next, rs_prefix, rs_after, rs_base) ->
    let prefix = to_string (getr st rs_prefix) in
    let after = to_string (getr st rs_after) in
    let base = Z.to_int (to_z (getr st rs_base)) in
    let plen = String.length prefix in
    let max_keys = 1000 in

    let suffixes = ref [] in
    Hashtbl.iter (fun k _v ->
      if not (is_reserved_key k)
         && String.length k >= plen && String.sub k 0 plen = prefix then begin
        let suffix = String.sub k plen (String.length k - plen) in

        if after = "" || String.compare suffix after > 0 then
          suffixes := suffix :: !suffixes
      end
    ) st.storage;

    let sorted = List.sort String.compare !suffixes in

    let count = ref 0 in
    let last_suffix = ref "" in
    let ok = ref true in
    List.iter (fun suffix ->
      if !ok && !count < max_keys then begin
        let idx = base + !count in
        if idx >= 0 && idx < 16_777_216 then begin
          if not (add_dyn_effort st 5) then ok := false
          else begin
            Hashtbl.replace st.memory.data idx (VString suffix);
            if idx >= st.memory.size then st.memory.size <- idx + 1;
            last_suffix := suffix;
            count := !count + 1
          end
        end
      end
    ) sorted;
    if not !ok then revert st
    else begin
    setr st rd_count (VInt (Z.of_int !count));

    let has_more = List.length sorted > !count in
    setr st rd_next (VString (if has_more then !last_suffix else ""));
    true
    end
  | SLOADN (rs_base_key, rs_base_val, rs_count) ->
    let base_key = Z.to_int (to_z (getr st rs_base_key)) in
    let base_val = Z.to_int (to_z (getr st rs_base_val)) in
    let count = Z.to_int (to_z (getr st rs_count)) in
    let count = min count 1000 in
    let ok = ref true in
    let i = ref 0 in
    while !ok && !i < count do
      let key_idx = base_key + !i in
      let val_idx = base_val + !i in
      let key = match Hashtbl.find_opt st.memory.data key_idx with
        | Some v -> to_string v | None -> "" in
      let v = match Hashtbl.find_opt st.storage key with
        | Some s -> VString s | None -> VString "" in
      Hashtbl.replace st.memory.data val_idx v;
      if val_idx >= st.memory.size then st.memory.size <- val_idx + 1;
      if not (add_dyn_effort st 15) then ok := false;
      incr i
    done;
    if not !ok then revert st else true
  | SSTOREN (rs_base_key, rs_base_val, rs_count) ->
    if not (view_guard st) then false
    else begin
      let base_key = Z.to_int (to_z (getr st rs_base_key)) in
      let base_val = Z.to_int (to_z (getr st rs_base_val)) in
      let count = Z.to_int (to_z (getr st rs_count)) in
      let count = min count 1000 in
      let ok = ref true in
      let i = ref 0 in
      while !ok && !i < count do
        let key_idx = base_key + !i in
        let val_idx = base_val + !i in
        let key = match Hashtbl.find_opt st.memory.data key_idx with
          | Some v -> to_string v | None -> "" in
        let value = match Hashtbl.find_opt st.memory.data val_idx with
          | Some v -> to_string v | None -> "" in
        if String.length key > 0 && String.length value <= max_storage_value_len
           && not (is_reserved_key key) then begin
          let old_val = Hashtbl.find_opt st.storage key in
          st.undo_stack <- UndoWrite (key, old_val) :: st.undo_stack;
          Hashtbl.replace st.storage key value
        end;
        if not (add_dyn_effort st 80) then ok := false;
        incr i
      done;
      if not !ok then revert st else true
    end
  | FSTORE (rd_hash, rs_data) ->
    if not (view_guard st) then false
    else begin
      let data = to_string (getr st rs_data) in
      let max_blob = 10_485_760 in
      if String.length data > max_blob then (st.reverted <- true; false)
      else begin
        let hash = Digestif.SHA256.(to_hex (digest_string data)) in
        Hashtbl.replace st.blobs hash data;
        if not (add_dyn_effort st (String.length data / 1024)) then revert st
        else (setr st rd_hash (VString hash); true)
      end
    end
  | FLOAD (rd_data, rs_hash) ->
    let hash = to_string (getr st rs_hash) in
    (match Hashtbl.find_opt st.blobs hash with
     | Some data ->
       if not (add_dyn_effort st (String.length data / 1024)) then revert st
       else (setr st rd_data (VString data); true)
     | None when st.strict_blobs -> revert st
     | None -> setr st rd_data (VString ""); true)
  | MATMUL (rd_addr, rs_lhs, rs_rhs, rs_m, rs_k, rs_n) ->
    let dst_addr = Z.to_int (to_z (getr st rd_addr)) in
    let lhs_addr = Z.to_int (to_z (getr st rs_lhs)) in
    let rhs_addr = Z.to_int (to_z (getr st rs_rhs)) in
    let m = Z.to_int (to_z (getr st rs_m)) in
    let k = Z.to_int (to_z (getr st rs_k)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    if m <= 0 || k <= 0 || n <= 0 || m > 32768 || k > 32768 || n > 32768 then revert st
    else begin
      if not (add_dyn_product st [m; n; k] 1024) then revert st
      else begin
        let mem_get a =
          match Hashtbl.find_opt st.memory.data a with
          | Some v -> to_z v
          | None -> Z.zero
        in
        for r = 0 to m - 1 do
          for c = 0 to n - 1 do
            let acc = ref Z.zero in
            for i = 0 to k - 1 do
              let lv = mem_get (lhs_addr + r * k + i) in
              let rv = mem_get (rhs_addr + i * n + c) in
              acc := Z.add !acc (Z.mul lv rv)
            done;
            let cell = dst_addr + r * n + c in
            Hashtbl.replace st.memory.data cell (VInt !acc);
            if cell >= st.memory.size then st.memory.size <- cell + 1
          done
        done;
        true
      end
    end
  | VECDOT (rd, rs_a, rs_b, rs_n) ->
    let a_addr = Z.to_int (to_z (getr st rs_a)) in
    let b_addr = Z.to_int (to_z (getr st rs_b)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    if n <= 0 || n > 131072 then revert st
    else begin
      if not (add_dyn_effort st (n / 16)) then revert st
      else begin
        let mem_get a =
          match Hashtbl.find_opt st.memory.data a with
          | Some v -> to_z v
          | None -> Z.zero
        in
        let acc = ref Z.zero in
        for i = 0 to n - 1 do
          let av = mem_get (a_addr + i) in
          let bv = mem_get (b_addr + i) in
          acc := Z.add !acc (Z.mul av bv)
        done;
        setr st rd (VInt !acc);
        true
      end
    end
  | VECDOT_Q16 (rd, rs_a, rs_b, rs_n) ->
    (match read_int st rs_a, read_int st rs_b, read_int st rs_n with
     | Some a_addr, Some b_addr, Some n
       when valid_mem_span a_addr n && valid_mem_span b_addr n ->
       if not (add_dyn_effort st (n / 16)) then revert st
       else
         (match read_q16 st a_addr n, read_q16 st b_addr n with
          | Some left, Some right ->
            let value = Fixed_q16.dot left 0 right 0 n in
            if Fixed_q16.in_range value then begin
              setr st rd (VInt value);
              true
            end else revert st
          | _ -> revert st)
     | _ -> revert st)
  | EXP_LUT (rd, rs_x) ->
    let x = to_z (getr st rs_x) in
    let q_one = 65536 in
    let lut_range_min = -8 * q_one in
    let lut_range_max = 8 * q_one in
    let xi = if Z.fits_int x then Z.to_int x else 0 in
    let xi = max lut_range_min (min lut_range_max xi) in
    let f = float_of_int xi /. float_of_int q_one in
    let e = exp f in
    let result = round_float_to_int (e *. float_of_int q_one) in
    setr st rd (VInt (Z.of_int result));
    true
  | EXP_Q16 (rd, rs_x) ->
    let x = to_z (getr st rs_x) in
    if not (add_dyn_effort st 8) then revert st
    else begin
      setr st rd (VInt (Fixed_q16.exp x));
      true
    end
  | SOFTMAX_INPLACE (rs_addr, rs_n) ->
    let addr = Z.to_int (to_z (getr st rs_addr)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    if n <= 0 || n > 131072 then revert st
    else begin
      if not (add_dyn_product st [n; 8] 1) then revert st
      else begin
        let q_one = 65536.0 in
        let mem_get a =
          match Hashtbl.find_opt st.memory.data a with
          | Some v ->
            let z = to_z v in
            if Z.fits_int z then Z.to_int z else 0
          | None -> 0
        in
        let arr = Array.init n (fun i -> mem_get (addr + i)) in
        let max_v = Array.fold_left max arr.(0) arr in
        let exps = Array.map (fun v ->
          let f = float_of_int (v - max_v) /. q_one in
          exp f
        ) arr in
        let sum = Array.fold_left (+.) 0.0 exps in
        if sum <= 0.0 then revert st
        else begin
          for i = 0 to n - 1 do
            let prob = exps.(i) /. sum in
            let qv = round_float_to_int (prob *. q_one) in
            Hashtbl.replace st.memory.data (addr + i) (VInt (Z.of_int qv))
          done;
          true
        end
      end
    end
  | SOFTMAX_Q16_INPLACE (rs_addr, rs_n) ->
    (match read_int st rs_addr, read_int st rs_n with
     | Some addr, Some n when valid_mem_span addr n ->
       if not (add_dyn_product st [n; 8] 1) then revert st
       else
         (match read_q16 st addr n with
          | None -> revert st
          | Some values ->
            let max_value = Array.fold_left Z.max values.(0) values in
            let exps = Array.map (fun value -> Fixed_q16.exp (Z.sub value max_value)) values in
            let total = Array.fold_left Z.add Z.zero exps in
            if Z.sign total <= 0 then revert st
            else
              let probs = Array.map (fun value -> Fixed_q16.scale_floor value total) exps in
              if not (Array.for_all Option.is_some probs) then revert st
              else
                let result = Array.map Option.get probs in
                let max_index = ref 0 in
                for i = 1 to n - 1 do
                  if Z.compare exps.(i) exps.(!max_index) > 0 then max_index := i
                done;
                let current = Array.fold_left Z.add Z.zero result in
                result.(!max_index) <-
                  Z.add result.(!max_index) (Z.sub Fixed_q16.scale current);
                if not (Array.for_all Fixed_q16.in_range result) then revert st
                else begin
                  write_q16 st addr result;
                  true
                end)
     | _ -> revert st)
  | LAYERNORM_Q16_INPLACE (rs_addr, rs_n, rs_gamma, rs_beta) ->
    (match read_int st rs_addr, read_int st rs_n,
           read_int st rs_gamma, read_int st rs_beta with
     | Some addr, Some n, Some gamma_addr, Some beta_addr
       when valid_mem_span addr n && valid_mem_span gamma_addr n &&
            valid_mem_span beta_addr n ->
       if not (add_dyn_product st [n; 4] 1) then revert st
       else
         (match read_q16 st addr n, read_q16 st gamma_addr n,
                read_q16 st beta_addr n with
          | Some values, Some gamma, Some beta ->
            (match Fixed_q16.layer values gamma beta with
             | Some result -> write_q16 st addr result; true
             | None -> revert st)
          | _ -> revert st)
     | _ -> revert st)
  | LAYERNORM_INPLACE (rs_addr, rs_n, rs_gamma, rs_beta) ->
    let addr = Z.to_int (to_z (getr st rs_addr)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    let gamma_addr = Z.to_int (to_z (getr st rs_gamma)) in
    let beta_addr = Z.to_int (to_z (getr st rs_beta)) in
    if n <= 0 || n > 131072 then revert st
    else begin
      if not (add_dyn_product st [n; 4] 1) then revert st
      else begin
        let q_one = 65536.0 in
        let mem_get_f a =
          match Hashtbl.find_opt st.memory.data a with
          | Some v ->
            let z = to_z v in
            if Z.fits_int z then float_of_int (Z.to_int z) /. q_one else 0.0
          | None -> 0.0
        in
        let arr = Array.init n (fun i -> mem_get_f (addr + i)) in
        let mean = Array.fold_left (+.) 0.0 arr /. float_of_int n in
        let var = Array.fold_left (fun acc v -> acc +. (v -. mean) ** 2.0) 0.0 arr /. float_of_int n in
        let inv_std = 1.0 /. sqrt (var +. 1e-5) in
        for i = 0 to n - 1 do
          let g = mem_get_f (gamma_addr + i) in
          let b = mem_get_f (beta_addr + i) in
          let normalized = (arr.(i) -. mean) *. inv_std in
          let result = g *. normalized +. b in
          let qv = round_float_to_int (result *. q_one) in
          Hashtbl.replace st.memory.data (addr + i) (VInt (Z.of_int qv))
        done;
        true
      end
    end
  | RELU_INPLACE (rs_addr, rs_n) ->
    let addr = Z.to_int (to_z (getr st rs_addr)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    if n <= 0 || n > 1048576 then revert st
    else begin
      if not (add_dyn_effort st (n / 4)) then revert st
      else begin
        for i = 0 to n - 1 do
          let cell = addr + i in
          match Hashtbl.find_opt st.memory.data cell with
          | Some v ->
            let z = to_z v in
            if Z.sign z < 0 then
              Hashtbl.replace st.memory.data cell (VInt Z.zero)
          | None -> ()
        done;
        true
      end
    end
  | RMSNORM_INPLACE (rs_addr, rs_n, rs_gamma) ->
    let addr = Z.to_int (to_z (getr st rs_addr)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    let gamma_addr = Z.to_int (to_z (getr st rs_gamma)) in
    if n <= 0 || n > 131072 then revert st
    else begin
      if not (add_dyn_product st [n; 3] 1) then revert st
      else begin
        let q_one = 65536.0 in
        let mem_get_f a =
          match Hashtbl.find_opt st.memory.data a with
          | Some v ->
            let z = to_z v in
            if Z.fits_int z then float_of_int (Z.to_int z) /. q_one else 0.0
          | None -> 0.0
        in
        let arr = Array.init n (fun i -> mem_get_f (addr + i)) in
        let sum_sq = Array.fold_left (fun acc v -> acc +. v *. v) 0.0 arr in
        let mean_sq = sum_sq /. float_of_int n in
        let inv_rms = 1.0 /. sqrt (mean_sq +. 1e-6) in
        for i = 0 to n - 1 do
          let g = mem_get_f (gamma_addr + i) in
          let result = g *. arr.(i) *. inv_rms in
          let qv = round_float_to_int (result *. q_one) in
          Hashtbl.replace st.memory.data (addr + i) (VInt (Z.of_int qv))
        done;
        true
      end
    end
  | RMSNORM_Q16_INPLACE (rs_addr, rs_n, rs_gamma) ->
    (match read_int st rs_addr, read_int st rs_n, read_int st rs_gamma with
     | Some addr, Some n, Some gamma_addr
       when valid_mem_span addr n && valid_mem_span gamma_addr n ->
       if not (add_dyn_product st [n; 3] 1) then revert st
       else
         (match read_q16 st addr n, read_q16 st gamma_addr n with
          | Some values, Some gamma ->
            (match Fixed_q16.rms values gamma with
             | Some result -> write_q16 st addr result; true
             | None -> revert st)
          | _ -> revert st)
     | _ -> revert st)
  | SILU_INPLACE (rs_addr, rs_n) ->
    let addr = Z.to_int (to_z (getr st rs_addr)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    if n <= 0 || n > 131072 then revert st
    else begin
      if not (add_dyn_product st [n; 2] 1) then revert st
      else begin
        let q_one = 65536.0 in
        for i = 0 to n - 1 do
          let cell = addr + i in
          match Hashtbl.find_opt st.memory.data cell with
          | Some v ->
            let z = to_z v in
            if Z.fits_int z then begin
              let x = float_of_int (Z.to_int z) /. q_one in
              let sigmoid = 1.0 /. (1.0 +. exp (-. x)) in
              let result = x *. sigmoid in
              let qv = round_float_to_int (result *. q_one) in
              Hashtbl.replace st.memory.data cell (VInt (Z.of_int qv))
            end
          | None -> ()
        done;
        true
      end
    end
  | SILU_Q16_INPLACE (rs_addr, rs_n) ->
    (match read_int st rs_addr, read_int st rs_n with
     | Some addr, Some n when valid_mem_span addr n ->
       if not (add_dyn_product st [n; 2] 1) then revert st
       else
         (match read_q16 st addr n with
          | None -> revert st
          | Some values ->
            let result = Array.map Fixed_q16.silu values in
            if not (Array.for_all Option.is_some result) then revert st
            else begin
              write_q16 st addr (Array.map Option.get result);
              true
            end)
     | _ -> revert st)
  | ELEMWISE_MUL_INPLACE (rs_dst, rs_src, rs_n) ->
    let dst = Z.to_int (to_z (getr st rs_dst)) in
    let src = Z.to_int (to_z (getr st rs_src)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    if n <= 0 || n > 1048576 then revert st
    else begin
      if not (add_dyn_effort st (n / 2)) then revert st
      else begin
        for i = 0 to n - 1 do
          let a_z = match Hashtbl.find_opt st.memory.data (dst + i) with
            | Some v -> to_z v | None -> Z.zero in
          let b_z = match Hashtbl.find_opt st.memory.data (src + i) with
            | Some v -> to_z v | None -> Z.zero in
          Hashtbl.replace st.memory.data (dst + i)
            (VInt (Fixed_q16.trunc_mul a_z b_z))
        done;
        true
      end
    end
  | ELEMWISE_MUL_Q16 (rs_dst, rs_src, rs_n) ->
    (match read_int st rs_dst, read_int st rs_src, read_int st rs_n with
     | Some dst, Some src, Some n
       when valid_mem_span dst n && valid_mem_span src n ->
       if not (add_dyn_effort st (n / 2)) then revert st
       else
         (match read_q16 st dst n, read_q16 st src n with
          | Some left, Some right ->
            (match Fixed_q16.elementwise_mul left right with
             | Some result -> write_q16 st dst result; true
             | None -> revert st)
          | _ -> revert st)
     | _ -> revert st)
  | LOAD_INT8_BYTES_TO_MEM (rs_dst, rs_src, rs_off, rs_n, rs_scale) ->
    let dst = Z.to_int (to_z (getr st rs_dst)) in
    let src_str = to_string (getr st rs_src) in
    let off = Z.to_int (to_z (getr st rs_off)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    let scale_z = to_z (getr st rs_scale) in
    let slen = String.length src_str in
    if n <= 0 || n > 1_048_576 then revert st
    else if off < 0 || off + n > slen then revert st
    else begin
      if not (add_dyn_effort st (n / 2)) then revert st
      else begin
        for i = 0 to n - 1 do
          let b = Char.code src_str.[off + i] in
          let signed = if b >= 128 then b - 256 else b in
          let v = Z.mul (Z.of_int signed) scale_z in
          Hashtbl.replace st.memory.data (dst + i) (VInt v)
        done;
        true
      end
    end
  | RESIDUAL_ADD (rs_dst, rs_src, rs_n) ->
    let dst = Z.to_int (to_z (getr st rs_dst)) in
    let src = Z.to_int (to_z (getr st rs_src)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    if n <= 0 || n > 1_048_576 then revert st
    else begin
      if not (add_dyn_effort st (n / 4)) then revert st
      else begin
        for i = 0 to n - 1 do
          let a_z = match Hashtbl.find_opt st.memory.data (dst + i) with
            | Some v -> to_z v | None -> Z.zero in
          let b_z = match Hashtbl.find_opt st.memory.data (src + i) with
            | Some v -> to_z v | None -> Z.zero in
          Hashtbl.replace st.memory.data (dst + i) (VInt (Z.add a_z b_z))
        done;
        true
      end
    end
  | RESIDUAL_ADD_Q16 (rs_dst, rs_src, rs_n) ->
    (match read_int st rs_dst, read_int st rs_src, read_int st rs_n with
     | Some dst, Some src, Some n
       when valid_mem_span dst n && valid_mem_span src n ->
       if not (add_dyn_effort st (n / 4)) then revert st
       else
         (match read_q16 st dst n, read_q16 st src n with
          | Some left, Some right ->
            (match Fixed_q16.residual_add left right with
             | Some result -> write_q16 st dst result; true
             | None -> revert st)
          | _ -> revert st)
     | _ -> revert st)
  | MATMUL_Q16 (rd_addr, rs_lhs, rs_rhs, rs_m, rs_k, rs_n) ->
    (match read_int st rd_addr, read_int st rs_lhs, read_int st rs_rhs,
           read_int st rs_m, read_int st rs_k, read_int st rs_n with
     | Some dst_addr, Some lhs_addr, Some rhs_addr, Some m, Some k, Some n
       when m > 0 && k > 0 && n > 0 && m <= 32768 && k <= 32768 && n <= 32768 ->
       (match checked_product m k, checked_product k n, checked_product m n with
        | Some lhs_n, Some rhs_n, Some dst_n
          when valid_mem_span lhs_addr lhs_n && valid_mem_span rhs_addr rhs_n &&
               valid_mem_span dst_addr dst_n ->
          if not (add_dyn_product st [m; n; k] 1024) then revert st
          else
            (match read_q16 st lhs_addr lhs_n, read_q16 st rhs_addr rhs_n with
             | Some lhs, Some rhs ->
               let result = Array.init dst_n (fun cell ->
                 let row = cell / n in
                 let col = cell mod n in
                 let acc = ref Z.zero in
                 for i = 0 to k - 1 do
                   let left = lhs.(row * k + i) in
                   let right = rhs.(i * n + col) in
                   acc := Z.add !acc (Z.mul left right)
                 done;
                 Fixed_q16.round16 !acc) in
               if not (Array.for_all Fixed_q16.in_range result) then revert st
               else begin
                 write_q16 st dst_addr result;
                 true
               end
             | _ -> revert st)
        | _ -> revert st)
     | _ -> revert st)
  | SHIFT_ROUND_INPLACE (rs_addr, rs_n, rs_bits) ->
    let addr = Z.to_int (to_z (getr st rs_addr)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    let bits = Z.to_int (to_z (getr st rs_bits)) in
    if n <= 0 || n > 1_048_576 || bits <= 0 || bits > 62 then revert st
    else begin
      if not (add_dyn_effort st (n / 4)) then revert st
      else begin
        match Fixed_q16.make_round bits with
        | None -> revert st
        | Some round ->
          for i = 0 to n - 1 do
            let v = match Hashtbl.find_opt st.memory.data (addr + i) with
              | Some x -> to_z x | None -> Z.zero in
            Hashtbl.replace st.memory.data (addr + i) (VInt (round v))
          done;
          true
      end
    end
  | LOAD_INT8_B64_TO_MEM (rs_dst, rs_src, rs_off, rs_n, rs_scale) ->
    let dst = Z.to_int (to_z (getr st rs_dst)) in
    let src_b64 = to_string (getr st rs_src) in
    let off = Z.to_int (to_z (getr st rs_off)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    let scale_z = to_z (getr st rs_scale) in
    if n <= 0 || n > 1_048_576 then revert st
    else begin
      match Base64.decode src_b64 with
      | Error _ -> revert st
      | Ok decoded ->
        let dlen = String.length decoded in
        if off < 0 || off + n > dlen then revert st
        else begin
          if not (add_dyn_effort st (n + dlen / 4)) then revert st
          else begin
            for i = 0 to n - 1 do
              let b = Char.code decoded.[off + i] in
              let signed = if b >= 128 then b - 256 else b in
              let v = Z.mul (Z.of_int signed) scale_z in
              Hashtbl.replace st.memory.data (dst + i) (VInt v)
            done;
            true
          end
        end
    end
  | LOAD_INT8_Q16 (rs_dst, rs_src, rs_off, rs_n, rs_scale) ->
    (match read_int st rs_dst, read_int st rs_off, read_int st rs_n with
     | Some dst, Some off, Some n
       when n > 0 && n <= 1_048_576 && valid_mem_span dst n ->
       let src_b64 = to_string (getr st rs_src) in
       let scale = to_z (getr st rs_scale) in
       if not (Fixed_q16.in_range scale) then revert st
       else
         (match Base64.decode src_b64 with
          | Error _ -> revert st
          | Ok decoded ->
            let dlen = String.length decoded in
            if off < 0 || off > dlen || n > dlen - off then revert st
            else if not (add_dyn_effort st (n + dlen / 4)) then revert st
            else
              let values = Array.init n (fun i ->
                let byte = Char.code decoded.[off + i] in
                let signed = if byte >= 128 then byte - 256 else byte in
                Z.mul (Z.of_int signed) scale) in
              if Array.for_all Fixed_q16.in_range values then begin
                write_q16 st dst values;
                true
              end else revert st)
     | _ -> revert st)
  | APPEND_VEC_Q16 (rs_dst, rs_pos, rs_src, rs_n) ->
    (match read_int st rs_dst, read_int st rs_pos, read_int st rs_src, read_int st rs_n with
     | Some dst, Some pos, Some src, Some n
       when pos >= 0 && pos <= 16_777_216 && valid_mem_span src n ->
       if pos > max_int / n then revert st
       else
         let target = dst + pos * n in
         if target < dst || not (valid_mem_span target n) then revert st
         else if not (add_dyn_effort st n) then revert st
         else
           (match read_q16 st src n with
            | Some values -> write_q16 st target values; true
            | None -> revert st)
     | _ -> revert st)
  | ARGMAX_Q16 (rd, rs_addr, rs_n) ->
    (match read_int st rs_addr, read_int st rs_n with
     | Some addr, Some n when valid_mem_span addr n ->
       if not (add_dyn_effort st (n / 2)) then revert st
       else
         (match read_q16 st addr n with
          | Some values ->
            let best = ref 0 in
            for index = 1 to n - 1 do
              if Z.compare values.(index) values.(!best) > 0 then best := index
            done;
            setr st rd (VInt (Z.of_int !best));
            true
          | None -> revert st)
     | _ -> revert st)
  | ROPE_APPLY (rs_addr, rs_n_dim, rs_pos, rs_base) ->
    let addr = Z.to_int (to_z (getr st rs_addr)) in
    let n_dim = Z.to_int (to_z (getr st rs_n_dim)) in
    let pos = Z.to_int (to_z (getr st rs_pos)) in
    let base_q = to_z (getr st rs_base) in
    if n_dim <= 0 || n_dim > 131072 || (n_dim land 1) <> 0 then revert st
    else if pos < 0 then revert st
    else begin
      if not (add_dyn_product st [n_dim; 4] 1) then revert st
      else begin
        let q_one = 65536.0 in
        let base_f =
          if Z.fits_int base_q then float_of_int (Z.to_int base_q) /. q_one
          else 10000.0 in
        let half = n_dim / 2 in
        let mem_get_f a =
          match Hashtbl.find_opt st.memory.data a with
          | Some v ->
            let z = to_z v in
            if Z.fits_int z then float_of_int (Z.to_int z) /. q_one else 0.0
          | None -> 0.0
        in
        let pf = float_of_int pos in
        let nf = float_of_int n_dim in
        for i = 0 to half - 1 do
          let exp_term = (2.0 *. float_of_int i) /. nf in
          let inv_freq = 1.0 /. (base_f ** exp_term) in
          let angle = pf *. inv_freq in
          let c = cos angle in
          let s = sin angle in
          let x_re = mem_get_f (addr + i) in
          let x_im = mem_get_f (addr + i + half) in
          let new_re = x_re *. c -. x_im *. s in
          let new_im = x_re *. s +. x_im *. c in
          Hashtbl.replace st.memory.data (addr + i)
            (VInt (Z.of_int (round_float_to_int (new_re *. q_one))));
          Hashtbl.replace st.memory.data (addr + i + half)
            (VInt (Z.of_int (round_float_to_int (new_im *. q_one))))
        done;
        true
      end
    end
  | ROPE_APPLY_Q16 (rs_addr, rs_n_dim, rs_pos, rs_base) ->
    (match read_int st rs_addr, read_int st rs_n_dim, read_int st rs_pos with
     | Some addr, Some n_dim, Some pos
       when pos >= 0 && n_dim > 0 && n_dim mod 2 = 0 && valid_mem_span addr n_dim ->
       let base = to_z (getr st rs_base) in
       if not (add_dyn_product st [n_dim; 4] 1) then revert st
       else
         (match read_q16 st addr n_dim with
          | Some values ->
            (match Fixed_q16.rope values base pos with
             | Some result -> write_q16 st addr result; true
             | None -> revert st)
          | None -> revert st)
     | _ -> revert st)
  | LINEAR_Q1_G128_FP (rs_dst, rs_lhs, rs_q1, rs_off, rs_m, rs_k, rs_n) ->
    (match read_int st rs_dst, read_int st rs_lhs, to_bytes (getr st rs_q1),
           read_int st rs_off, read_int st rs_m, read_int st rs_k,
           read_int st rs_n with
     | Some dst, Some lhs, Some q1, Some off, Some m, Some k, Some n
       when off >= 0 && m > 0 && k > 0 && n > 0 && k mod 128 = 0
            && m <= 32768 && k <= 32768 && n <= 32768 ->
       let group = 128 in
       let block_bytes = 18 in
       let blocks_per_output = k / group in
       (match checked_product m k, checked_product m n,
              checked_product n blocks_per_output with
        | Some lhs_n, Some dst_n, Some blocks ->
          (match checked_product blocks block_bytes with
           | Some q1_n
             when valid_mem_span lhs lhs_n && valid_mem_span dst dst_n
                  && off <= String.length q1
                  && q1_n <= String.length q1 - off ->
             if not (add_dyn_product st [m; n; k] 512) then revert st
             else
               (match read_fp64_bits_array st.memory.data lhs lhs_n with
               | None -> revert st
               | Some lhs_values ->
                 (match decode_q1_g128_scale_bits q1 off blocks with
                  | None -> revert st
                  | Some scale_bits ->
                    let output = Array.make dst_n 0L in
                    let ok = ref true in
                    for row = 0 to m - 1 do
                      for col = 0 to n - 1 do
                        let acc = ref 0L in
                        for block = 0 to blocks_per_output - 1 do
                          let q1_block = (col * blocks_per_output) + block in
                          let scale = Array.unsafe_get scale_bits q1_block in
                          let block_offset = off + (q1_block * block_bytes) in
                          for item = 0 to group - 1 do
                            let sign_byte =
                              Char.code q1.[block_offset + 2 + (item lsr 3)]
                            in
                            let scale =
                              if (sign_byte lsr (item land 7)) land 1 = 1 then
                                scale
                              else
                                Inference_fp64.negate scale
                            in
                            let lhs_value =
                              Array.unsafe_get
                                lhs_values
                                ((row * k) + (block * group) + item)
                            in
                            match Inference_fp64.mul lhs_value scale with
                            | Some product ->
                              (match Inference_fp64.add !acc product with
                               | Some next -> acc := next
                               | None -> ok := false)
                            | None -> ok := false
                          done
                        done;
                        Array.unsafe_set output ((row * n) + col) !acc
                      done
                    done;
                    if not !ok then revert st
                    else begin
                      for i = 0 to dst_n - 1 do
                        mem_set_fp64_bits st.memory.data
                          (dst + i)
                          (Array.unsafe_get output i)
                      done;
                      true
                    end))
           | _ -> revert st)
        | _ -> revert st)
     | _ -> revert st)
  | LOAD_F32_LE_FP (rs_dst, rs_src, rs_off, rs_n) ->
    (match read_int st rs_dst, to_bytes (getr st rs_src),
           read_int st rs_off, read_int st rs_n with
     | Some dst, Some src, Some off, Some n
       when off >= 0 && valid_mem_span dst n
            && off <= String.length src
            && n <= (String.length src - off) / 4 ->
       if not (add_dyn_effort st n) then revert st
       else
         (* Bits-only path: finite f32→f64 bit widen, no host-float dual-write. *)
         let decoded = Array.make n 0L in
         let ok = ref true in
         for i = 0 to n - 1 do
           match f32_le_to_fp64_bits src (off + (i * 4)) with
           | None -> ok := false
           | Some bits -> decoded.(i) <- bits
         done;
         if not !ok then revert st
         else begin
           for i = 0 to n - 1 do
             mem_set_fp64_bits st.memory.data (dst + i) decoded.(i)
           done;
           true
       end
     | _ -> revert st)
  | LOAD_F64_LE_FP (rs_dst, rs_src, rs_off, rs_n) ->
    (match read_int st rs_dst, to_bytes (getr st rs_src),
           read_int st rs_off, read_int st rs_n with
     | Some dst, Some src, Some off, Some n
       when off >= 0 && valid_mem_span dst n
            && off <= String.length src
            && n <= (String.length src - off) / 8 ->
       if not (add_dyn_effort st n) then revert st
       else
         (* Spine-safe path: bit-exact finite binary64 cells, no host float writeback. *)
         let decoded = Array.make n 0L in
         let ok = ref true in
         for i = 0 to n - 1 do
           match f64_le_to_fp64_bits src (off + (i * 8)) with
           | None -> ok := false
           | Some bits -> decoded.(i) <- bits
         done;
         if not !ok then revert st
         else begin
           for i = 0 to n - 1 do
             mem_set_fp64_bits st.memory.data (dst + i) decoded.(i)
           done;
           true
       end
     | _ -> revert st)
  | SIGMOID_FP (rs_addr, rs_n) ->
    (match read_int st rs_addr, read_int st rs_n with
     | Some addr, Some n ->
       map_fp64_bits_inplace st addr n fp64_sigmoid_bits
     | _ -> revert st)
  | SOFTPLUS_FP (rs_addr, rs_n) ->
    (match read_int st rs_addr, read_int st rs_n with
     | Some addr, Some n ->
       map_fp64_bits_inplace st addr n fp64_softplus_bits
     | _ -> revert st)
  | CAUSAL_DEPTHWISE_CONV1D_FP
      (rs_dst, rs_input, rs_kernel, rs_t, rs_c, rs_w) ->
    (match read_int st rs_dst, read_int st rs_input, read_int st rs_kernel,
           read_int st rs_t, read_int st rs_c, read_int st rs_w with
     | Some dst, Some input, Some kernel, Some timesteps, Some channels,
       Some width
       when timesteps > 0 && channels > 0 && width > 0 ->
       (match checked_product timesteps channels,
              checked_product channels width with
        | Some values_n, Some kernel_n
          when valid_mem_span dst values_n
               && valid_mem_span input values_n
               && valid_mem_span kernel kernel_n ->
          if not (add_dyn_product st [timesteps; channels; width] 1) then
            revert st
          else
            (match read_fp64_bits_array st.memory.data input values_n,
                   read_fp64_bits_array st.memory.data kernel kernel_n with
             | Some input_values, Some kernel_values ->
               let output = Array.make values_n 0L in
               let ok = ref true in
               for t = 0 to timesteps - 1 do
                 for c = 0 to channels - 1 do
                   let acc = ref 0L in
                   for k = 0 to width - 1 do
                     if t >= k then
                       match
                         Inference_fp64.mul
                           (Array.unsafe_get
                              input_values
                              (((t - k) * channels) + c))
                           (Array.unsafe_get
                              kernel_values
                              ((c * width) + k))
                       with
                       | Some product ->
                         (match Inference_fp64.add !acc product with
                          | Some next -> acc := next
                          | None -> ok := false)
                       | None -> ok := false
                   done;
                   Array.unsafe_set output ((t * channels) + c) !acc
                 done
               done;
               if not !ok then revert st
               else begin
                 for i = 0 to values_n - 1 do
                   mem_set_fp64_bits
                     st.memory.data
                     (dst + i)
                     (Array.unsafe_get output i)
                 done;
                 true
               end
             | _ -> revert st)
        | _ -> revert st)
     | _ -> revert st)
  | GATED_DELTA_RULE_FP
      (rs_output, rs_state_dst, rs_q, rs_k, rs_v, rs_log_decay, rs_beta,
       rs_state, rs_t, rs_qh, rs_kh, rs_vh, rs_kd, rs_vd) ->
    (match read_int st rs_output, read_int st rs_state_dst, read_int st rs_q,
           read_int st rs_k, read_int st rs_v, read_int st rs_log_decay,
           read_int st rs_beta, read_int st rs_state, read_int st rs_t,
           read_int st rs_qh, read_int st rs_kh, read_int st rs_vh,
           read_int st rs_kd, read_int st rs_vd with
     | Some output, Some state_dst, Some q, Some k, Some v, Some log_decay,
       Some beta, Some state_src, Some timesteps, Some q_heads, Some k_heads,
       Some v_heads, Some key_dim, Some value_dim
       when timesteps > 0 && q_heads > 0 && k_heads > 0 && v_heads > 0
            && key_dim > 0 && value_dim > 0 ->
       (match checked_product_many [timesteps; q_heads; key_dim],
              checked_product_many [timesteps; k_heads; key_dim],
              checked_product_many [timesteps; v_heads; value_dim],
              checked_product timesteps v_heads,
              checked_product_many [v_heads; value_dim; key_dim] with
        | Some q_n, Some k_n, Some v_n, Some gate_n, Some state_n ->
          let output_n = v_n in
          let input_spans =
            [q, q_n; k, k_n; v, v_n; log_decay, gate_n; beta, gate_n]
          in
          let spans =
            input_spans @ [state_src, state_n; output, output_n; state_dst, state_n]
          in
          if not (List.for_all (fun (addr, n) -> valid_large_mem_span addr n) spans) then
            revert st
          else
            let input_aliases_writable (addr, n) =
              ranges_overlap addr n output output_n
              || ranges_overlap addr n state_dst state_n
            in
            let state_alias_invalid =
              ranges_overlap state_src state_n output output_n
              || (ranges_overlap state_src state_n state_dst state_n
                  && not (same_range state_src state_n state_dst state_n))
            in
            if ranges_overlap output output_n state_dst state_n
               || List.exists input_aliases_writable input_spans
               || state_alias_invalid then
              revert st
            else
              (match gated_delta_rule_effort timesteps v_heads value_dim key_dim with
              | None -> revert st
              | Some effort when not (add_dyn_effort st effort) -> revert st
              | Some _ ->
                (match read_fp64_bits_array st.memory.data q q_n,
                       read_fp64_bits_array st.memory.data k k_n,
                       read_fp64_bits_array st.memory.data v v_n,
                       read_fp64_bits_array st.memory.data log_decay gate_n,
                       read_fp64_bits_array st.memory.data beta gate_n,
                       read_fp64_bits_array st.memory.data state_src state_n with
                 | Some q_values, Some k_values, Some v_values,
                   Some log_decay_values, Some beta_values, Some state_values ->
                   let state_values = Array.copy state_values in
                   let output_values = Array.make output_n 0L in
                   let state_per_head = value_dim * key_dim in
                   let scale_bits, scale_ok =
                     match Inference_fp64.of_int key_dim with
                     | Some key_dim_bits ->
                       (match fp64_inverse_sqrt_bits key_dim_bits with
                        | Some bits -> bits, true
                        | None -> 0L, false)
                     | None -> 0L, false
                   in
                   let ok = ref scale_ok in
                   let mul_add acc left right =
                     match Inference_fp64.mul left right with
                     | Some product -> Inference_fp64.add acc product
                     | None -> None
                   in
                   for timestep = 0 to timesteps - 1 do
                     let q_t = timestep * q_heads * key_dim in
                     let k_t = timestep * k_heads * key_dim in
                     let v_t = timestep * v_heads * value_dim in
                     let gate_t = timestep * v_heads in
                     let out_t = timestep * v_heads * value_dim in
                     for head = 0 to v_heads - 1 do
                       let q_head = head mod q_heads in
                       let k_head = head mod k_heads in
                       let q_base = q_t + (q_head * key_dim) in
                       let k_base = k_t + (k_head * key_dim) in
                       let v_base = v_t + (head * value_dim) in
                       let state_base = head * state_per_head in
                       let decay_input_bits =
                         log_decay_values.(gate_t + head)
                       in
                       let decay_bits =
                         match
                          Inference_fp64.exp_nonpositive decay_input_bits
                         with
                         | Some decay_bits -> decay_bits
                         | None -> ok := false; 0L
                       in
                       for i = 0 to state_per_head - 1 do
                         match
                           Inference_fp64.mul
                             state_values.(state_base + i)
                             decay_bits
                         with
                         | Some value ->
                           state_values.(state_base + i) <- value
                         | None -> ok := false
                       done;
                       let delta = Array.make value_dim 0L in
                       for row = 0 to value_dim - 1 do
                         let row_base = state_base + (row * key_dim) in
                         let memory = ref 0L in
                         for col = 0 to key_dim - 1 do
                           match
                             mul_add
                               !memory
                               state_values.(row_base + col)
                               k_values.(k_base + col)
                           with
                           | Some value -> memory := value
                           | None -> ok := false
                         done;
                         let value =
                           match
                             Inference_fp64.sub
                               v_values.(v_base + row)
                               !memory
                           with
                           | Some diff ->
                             Inference_fp64.mul
                               diff
                               beta_values.(gate_t + head)
                           | None -> None
                         in
                         (match value with
                          | Some value -> delta.(row) <- value
                          | None -> ok := false)
                       done;
                       for row = 0 to value_dim - 1 do
                         let row_base = state_base + (row * key_dim) in
                         for col = 0 to key_dim - 1 do
                           match
                             mul_add
                               state_values.(row_base + col)
                               k_values.(k_base + col)
                               delta.(row)
                           with
                           | Some value ->
                             state_values.(row_base + col) <- value
                           | None -> ok := false
                         done
                       done;
                       let out_base = out_t + (head * value_dim) in
                       for row = 0 to value_dim - 1 do
                         let row_base = state_base + (row * key_dim) in
                         let value = ref 0L in
                         for col = 0 to key_dim - 1 do
                           match
                             mul_add
                               !value
                               state_values.(row_base + col)
                               q_values.(q_base + col)
                           with
                           | Some next -> value := next
                           | None -> ok := false
                         done;
                         (match Inference_fp64.mul !value scale_bits with
                          | Some scaled ->
                            output_values.(out_base + row) <- scaled
                          | None -> ok := false)
                       done
                     done
                   done;
                   if not !ok
                      || not (Array.for_all Inference_fp64.finite state_values)
                      || not (Array.for_all Inference_fp64.finite output_values)
                      || not (Inference_fp64.finite scale_bits) then
                     revert st
                   else begin
                     for i = 0 to output_n - 1 do
                       mem_set_fp64_bits
                         st.memory.data
                         (output + i)
                         output_values.(i)
                     done;
                     for i = 0 to state_n - 1 do
                       mem_set_fp64_bits
                         st.memory.data
                         (state_dst + i)
                         state_values.(i)
                     done;
                     true
                   end
                 | _ -> revert st))
        | _ -> revert st)
     | _ -> revert st)
  | MATMUL_FP (rd_addr, rs_lhs, rs_rhs, rs_m, rs_k, rs_n) ->
    let dst = Z.to_int (to_z (getr st rd_addr)) in
    let lhs = Z.to_int (to_z (getr st rs_lhs)) in
    let rhs = Z.to_int (to_z (getr st rs_rhs)) in
    let m = Z.to_int (to_z (getr st rs_m)) in
    let k = Z.to_int (to_z (getr st rs_k)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    if m <= 0 || k <= 0 || n <= 0 || m > 32768 || k > 32768 || n > 32768 then revert st
    else begin
      if not (add_dyn_product st [m; n; k] 512) then revert st
      else begin
        let lhs_arr = Array.make (m * k) 0.0 in
        for i = 0 to m * k - 1 do
          lhs_arr.(i) <- mem_get_fp64 st.memory.data (lhs + i)
        done;
        let rhs_arr = Array.make (k * n) 0.0 in
        for i = 0 to k * n - 1 do
          rhs_arr.(i) <- mem_get_fp64 st.memory.data (rhs + i)
        done;
        let dst_arr = Array.make (m * n) 0.0 in
        for r = 0 to m - 1 do
          let r_k = r * k in
          let r_n = r * n in
          for c = 0 to n - 1 do
            let acc = ref 0.0 in
            for i = 0 to k - 1 do
              acc := !acc +. (Array.unsafe_get lhs_arr (r_k + i)
                              *. Array.unsafe_get rhs_arr (i * n + c))
            done;
            Array.unsafe_set dst_arr (r_n + c) !acc
          done
        done;
        for i = 0 to m * n - 1 do
          mem_set_fp64 st.memory.data (dst + i) (Array.unsafe_get dst_arr i)
        done;
        true
      end
    end
  | RMSNORM_FP (rs_addr, rs_n, rs_gamma) ->
    let addr = Z.to_int (to_z (getr st rs_addr)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    let gamma = Z.to_int (to_z (getr st rs_gamma)) in
    if n <= 0 || n > 131072 then revert st
    else begin
      if not (add_dyn_product st [n; 4] 1) then revert st
      else begin
        let arr = Array.init n (fun i -> mem_get_fp64 st.memory.data (addr + i)) in
        let sum_sq = Array.fold_left (fun acc v -> acc +. v *. v) 0.0 arr in
        let mean_sq = sum_sq /. float_of_int n in
        let inv_rms = 1.0 /. sqrt (mean_sq +. 1e-5) in
        for i = 0 to n - 1 do
          let g = mem_get_fp64 st.memory.data (gamma + i) in
          mem_set_fp64 st.memory.data (addr + i) (g *. arr.(i) *. inv_rms)
        done;
        true
      end
    end
  | RMSNORM_FP_EPS (rs_addr, rs_n, rs_gamma, rs_epsilon) ->
    (match read_int st rs_addr, read_int st rs_n, read_int st rs_gamma,
           read_fp64_reg_bits st rs_epsilon with
     | Some addr, Some n, Some gamma, Some epsilon_bits
       when n > 0 && fp64_positive_bits epsilon_bits ->
       if not
            (List.for_all
               (fun (addr, n) -> valid_large_mem_span addr n)
               [addr, n; gamma, n])
          || ranges_overlap addr n gamma n then
         revert st
       else if not (add_dyn_product st [n; 4] 1) then
         revert st
       else
        (match read_fp64_bits_array st.memory.data addr n,
               read_fp64_bits_array st.memory.data gamma n with
         | Some input_values, Some gamma_values ->
            let ok = ref true in
            let sum_sq_bits = ref 0L in
            Array.iter
              (fun value ->
                 match Inference_fp64.square value with
                 | Some square ->
                   (match Inference_fp64.add !sum_sq_bits square with
                    | Some next -> sum_sq_bits := next
                    | None -> ok := false)
                 | None -> ok := false)
              input_values;
            let inv_rms_bits =
              match Inference_fp64.of_int n with
              | Some count_bits ->
                (match Inference_fp64.div !sum_sq_bits count_bits with
                 | Some mean_sq_bits ->
                   (match Inference_fp64.add mean_sq_bits epsilon_bits with
                    | Some inverse_input_bits ->
                      fp64_inverse_sqrt_bits inverse_input_bits
                    | None -> None)
                 | None -> None)
              | None -> None
            in
            let output =
              Array.init n (fun i ->
                match inv_rms_bits with
                | Some inv_rms_bits ->
                  (match Inference_fp64.mul input_values.(i) inv_rms_bits with
                   | Some scaled ->
                     Inference_fp64.mul scaled gamma_values.(i)
                   | None -> None)
                | None -> None)
            in
            if not !ok
               || Option.is_none inv_rms_bits
               || not (Array.for_all Option.is_some output) then
              revert st
            else begin
              for i = 0 to n - 1 do
                mem_set_fp64_bits
                  st.memory.data
                  (addr + i)
                  (Option.get output.(i))
              done;
              true
            end
          | _ -> revert st)
     | _ -> revert st)
  | L2NORM_FP (rs_addr, rs_n, rs_epsilon) ->
    (match read_int st rs_addr, read_int st rs_n,
           read_fp64_reg_bits st rs_epsilon with
     | Some addr, Some n, Some epsilon_bits
       when n > 0 && fp64_positive_bits epsilon_bits ->
       if not (valid_large_mem_span addr n) then
         revert st
       else if not (add_dyn_product st [n; 3] 1) then
         revert st
       else
        (match read_fp64_bits_array st.memory.data addr n with
         | Some input_values ->
           let ok = ref true in
           let sum_sq_bits = ref 0L in
           Array.iter
             (fun value ->
                match Inference_fp64.square value with
                | Some square ->
                  (match Inference_fp64.add !sum_sq_bits square with
                   | Some next -> sum_sq_bits := next
                   | None -> ok := false)
                | None -> ok := false)
             input_values;
           let inv_norm_bits =
             match Inference_fp64.add !sum_sq_bits epsilon_bits with
             | Some inverse_input_bits -> fp64_inverse_sqrt_bits inverse_input_bits
             | None -> None
           in
           let output =
             Array.init n (fun i ->
               match inv_norm_bits with
               | Some inv_norm_bits ->
                 Inference_fp64.mul input_values.(i) inv_norm_bits
               | None -> None)
           in
           if not !ok
              || Option.is_none inv_norm_bits
              || not (Array.for_all Option.is_some output) then
             revert st
           else begin
             for i = 0 to n - 1 do
               mem_set_fp64_bits
                 st.memory.data
                 (addr + i)
                 (Option.get output.(i))
             done;
             true
           end
          | _ -> revert st)
     | _ -> revert st)
  | SILU_FP (rs_addr, rs_n) ->
    (match read_int st rs_addr, read_int st rs_n with
     | Some addr, Some n ->
       map_fp64_bits_inplace st addr n fp64_silu_bits
     | _ -> revert st)
  | ELEMWISE_MUL_FP (rs_dst, rs_src, rs_n) ->
    (match read_int st rs_dst, read_int st rs_src, read_int st rs_n with
     | Some dst, Some src, Some n when n > 0 ->
       if not
            (List.for_all
               (fun (addr, n) -> valid_large_mem_span addr n)
               [dst, n; src, n])
          || (ranges_overlap dst n src n && not (same_range dst n src n)) then
         revert st
       else if not (add_dyn_product st [n; 3] 1) then
         revert st
       else
         (match read_fp64_bits_array st.memory.data dst n,
                read_fp64_bits_array st.memory.data src n with
          | Some dst_values, Some src_values ->
            let output =
              Array.init n (fun i ->
                Inference_fp64.mul dst_values.(i) src_values.(i))
            in
            if not (Array.for_all Option.is_some output) then revert st
            else begin
              for i = 0 to n - 1 do
                mem_set_fp64_bits
                  st.memory.data
                  (dst + i)
                  (Option.get output.(i))
              done;
              true
            end
          | _ -> revert st)
     | _ -> revert st)
  | RESIDUAL_ADD_FP (rs_dst, rs_src, rs_n) ->
    (match read_int st rs_dst, read_int st rs_src, read_int st rs_n with
     | Some dst, Some src, Some n when n > 0 ->
       if not
            (List.for_all
               (fun (addr, n) -> valid_large_mem_span addr n)
               [dst, n; src, n])
          || (ranges_overlap dst n src n && not (same_range dst n src n)) then
         revert st
       else if not (add_dyn_product st [n; 2] 1) then
         revert st
       else
         (match read_fp64_bits_array st.memory.data dst n,
                read_fp64_bits_array st.memory.data src n with
          | Some dst_values, Some src_values ->
            let output =
              Array.init n (fun i ->
                Inference_fp64.add dst_values.(i) src_values.(i))
            in
            if not (Array.for_all Option.is_some output) then revert st
            else begin
              for i = 0 to n - 1 do
                mem_set_fp64_bits
                  st.memory.data
                  (dst + i)
                  (Option.get output.(i))
              done;
              true
            end
          | _ -> revert st)
     | _ -> revert st)
  | ROPE_APPLY_FP (rs_addr, rs_n_dim, rs_pos, rs_base) ->
    let addr = Z.to_int (to_z (getr st rs_addr)) in
    let n_dim = Z.to_int (to_z (getr st rs_n_dim)) in
    let pos = Z.to_int (to_z (getr st rs_pos)) in
    let base_z = to_z (getr st rs_base) in
    if n_dim <= 0 || n_dim > 131072 || (n_dim land 1) <> 0 then revert st
    else if pos < 0 then revert st
    else begin
      if not (add_dyn_product st [n_dim; 8] 1) then revert st
      else begin
        let base_f = z_to_fp64 base_z in
        let half = n_dim / 2 in
        let pf = float_of_int pos in
        let nf = float_of_int n_dim in
        for i = 0 to half - 1 do
          let exp_term = (2.0 *. float_of_int i) /. nf in
          let inv_freq = 1.0 /. (base_f ** exp_term) in
          let angle = pf *. inv_freq in
          let c = cos angle in
          let s = sin angle in
          let x_re = mem_get_fp64 st.memory.data (addr + i) in
          let x_im = mem_get_fp64 st.memory.data (addr + i + half) in
          mem_set_fp64 st.memory.data (addr + i) (x_re *. c -. x_im *. s);
          mem_set_fp64 st.memory.data (addr + i + half) (x_re *. s +. x_im *. c)
        done;
        true
      end
    end
  | ROPE_APPLY_INDEXED_FP
      (rs_addr, rs_count, rs_head_dim, rs_rot_dim, rs_positions, rs_base) ->
    (match read_int st rs_addr, read_int st rs_count,
           read_int st rs_head_dim, read_int st rs_rot_dim,
           read_int st rs_positions,
           read_fp64_reg_bits st rs_base with
     | Some addr, Some count, Some head_dim, Some rot_dim,
       Some positions_addr, Some base_bits
       when count > 0 && head_dim > 0 && rot_dim > 0
            && rot_dim <= head_dim && rot_dim land 1 = 0
            && Inference_fp64.finite base_bits
            && Inference_fp64.compare base_bits fp64_one_bits = Some 1
            && count mod head_dim = 0 ->
       let pairs = rot_dim / 2 in
       if not
            (valid_large_mem_span addr count
             && valid_large_mem_span positions_addr pairs)
          || ranges_overlap addr count positions_addr pairs then
         revert st
       else
         let heads = count / head_dim in
         if not (add_dyn_effort st count) then
           revert st
         else if not (add_dyn_product st [heads; pairs; 8] 1) then
           revert st
         else
           (match read_fp64_bits_array st.memory.data addr count,
                  read_position_array st.memory.data positions_addr pairs with
            | Some input_values, Some positions ->
              let output = Array.copy input_values in
              let ok = ref true in
              (match Inference_fp64.ln_positive_fixed base_bits with
               | Some base_ln_fixed ->
                 for head = 0 to heads - 1 do
                   let base_addr = head * head_dim in
                   for i = 0 to pairs - 1 do
                     let exponent_ratio =
                       Z.div
                         (Z.mul (Z.of_int (2 * i)) base_ln_fixed)
                         (Z.of_int rot_dim)
                     in
                     (match
                        Inference_fp64.rope_theta_fixed
                          positions.(i)
                          base_ln_fixed
                          exponent_ratio
                      with
                      | Some theta_fixed ->
                        (match Inference_fp64.fixed_to_bits theta_fixed with
                         | Some theta_bits ->
                           (match Inference_fp64.sin_cos theta_bits with
                            | Some s, Some c ->
                              let left_index = base_addr + i in
                              let right_index = base_addr + i + pairs in
                              let left =
                                Array.unsafe_get input_values left_index
                              in
                              let right =
                                Array.unsafe_get input_values right_index
                              in
                              (match
                                 Inference_fp64.mul left c,
                                 Inference_fp64.mul right s,
                                 Inference_fp64.mul left s,
                                 Inference_fp64.mul right c
                               with
                               | Some left_c, Some right_s, Some left_s,
                                 Some right_c ->
                                 (match
                                    Inference_fp64.sub left_c right_s,
                                    Inference_fp64.add left_s right_c
                                  with
                                  | Some out_left, Some out_right ->
                                    Array.unsafe_set output left_index out_left;
                                    Array.unsafe_set output right_index out_right;
                                    if
                                      not
                                        (Inference_fp64.finite theta_bits
                                        && Inference_fp64.finite c
                                        && Inference_fp64.finite s)
                                    then ok := false
                                  | _ -> ok := false)
                               | _ -> ok := false)
                            | _ -> ok := false)
                         | None -> ok := false)
                      | None -> ok := false)
                   done
                 done
               | None -> ok := false);
              if not !ok || not (Array.for_all Inference_fp64.finite output) then
                revert st
              else begin
                for i = 0 to count - 1 do
                  mem_set_fp64_bits st.memory.data (addr + i) output.(i)
                done;
                true
              end
            | _ -> revert st)
     | _ -> revert st)
  | LOAD_INT8_FP (rs_dst, rs_src, rs_off, rs_n, rs_scale) ->
    let dst = Z.to_int (to_z (getr st rs_dst)) in
    let src_b64 = to_string (getr st rs_src) in
    let off = Z.to_int (to_z (getr st rs_off)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    let scale = z_to_fp64 (to_z (getr st rs_scale)) in
    if n <= 0 || n > 1_048_576 then revert st
    else begin
      let cache_key =
        let len = String.length src_b64 in
        let prefix_len = min 32 len in
        let prefix_hash = ref 0 in
        for i = 0 to prefix_len - 1 do
          prefix_hash := (!prefix_hash * 31 + Char.code src_b64.[i]) land 0x7fffffff
        done;
        len * 1000003 + !prefix_hash
      in
      let decoded_opt = Hashtbl.find_opt st.decoded_chunk_cache cache_key in
      let decoded = match decoded_opt with
        | Some d -> Some d
        | None ->
          (match Base64.decode src_b64 with
           | Ok d ->
             Hashtbl.replace st.decoded_chunk_cache cache_key d;
             Some d
           | Error _ -> None)
      in
      match decoded with
      | None -> revert st
      | Some decoded ->
        let dlen = String.length decoded in
        if off < 0 || off + n > dlen then revert st
        else begin
          let was_cached = decoded_opt <> None in
          let cost = if was_cached then n / 2 else n + dlen / 4 in
          if not (add_dyn_effort st cost) then revert st
          else begin
            for i = 0 to n - 1 do
              let b = Char.code decoded.[off + i] in
              let signed = if b >= 128 then b - 256 else b in
              mem_set_fp64 st.memory.data (dst + i) (float_of_int signed *. scale)
            done;
            true
          end
        end
    end
  | VECDOT_FP (rd, rs_a, rs_b, rs_n) ->
    let a = Z.to_int (to_z (getr st rs_a)) in
    let b = Z.to_int (to_z (getr st rs_b)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    if n <= 0 || n > 1_048_576 then revert st
    else begin
      if not (add_dyn_effort st (n / 8)) then revert st
      else begin
        let a_arr = Array.make n 0.0 in
        let b_arr = Array.make n 0.0 in
        for i = 0 to n - 1 do
          a_arr.(i) <- mem_get_fp64 st.memory.data (a + i);
          b_arr.(i) <- mem_get_fp64 st.memory.data (b + i)
        done;
        let acc = ref 0.0 in
        for i = 0 to n - 1 do
          acc := !acc +. (Array.unsafe_get a_arr i *. Array.unsafe_get b_arr i)
        done;
        setr st rd (VInt (fp64_to_z !acc));
        true
      end
    end
  | ATTENTION_SCORES_FP (rs_dst, rs_q, rs_k, rs_key_count, rs_head_dim) ->
    (match read_int st rs_dst, read_int st rs_q, read_int st rs_k,
           read_int st rs_key_count, read_int st rs_head_dim with
     | Some dst, Some query, Some key, Some key_count, Some head_dim
       when key_count > 0 && key_count <= 8192
            && head_dim > 0 && head_dim <= 1024 ->
       (match checked_product key_count head_dim with
        | Some key_cells
          when valid_large_mem_span dst key_count
               && valid_large_mem_span query head_dim
               && valid_large_mem_span key key_cells
               && not (ranges_overlap dst key_count query head_dim)
               && not (ranges_overlap dst key_count key key_cells) ->
          if not (add_dyn_product st [key_count; head_dim; 4] 1) then
            revert st
          else
            (match read_fp64_bits_array st.memory.data query head_dim,
                   read_fp64_bits_array st.memory.data key key_cells with
             | Some query_values, Some key_values ->
               let output = Array.make key_count 0L in
               let scale_bits, scale_ok =
                 match Inference_fp64.of_int head_dim with
                 | Some head_dim_bits ->
                   (match fp64_inverse_sqrt_bits head_dim_bits with
                    | Some bits -> bits, true
                    | None -> 0L, false)
                 | None -> 0L, false
               in
               let ok = ref scale_ok in
               for key_index = 0 to key_count - 1 do
                 let acc = ref 0L in
                 let key_base = key_index * head_dim in
                 for dim = 0 to head_dim - 1 do
                   match
                     Inference_fp64.mul
                       (Array.unsafe_get query_values dim)
                       (Array.unsafe_get key_values (key_base + dim))
                   with
                   | Some product ->
                     (match Inference_fp64.add !acc product with
                      | Some next -> acc := next
                      | None -> ok := false)
                   | None -> ok := false
                 done;
                 (match Inference_fp64.mul !acc scale_bits with
                  | Some score -> Array.unsafe_set output key_index score
                  | None -> ok := false)
               done;
               if not !ok then
                 revert st
               else begin
                 for index = 0 to key_count - 1 do
                   mem_set_fp64_bits
                     st.memory.data
                     (dst + index)
                     (Array.unsafe_get output index)
                 done;
                 true
               end
             | _ -> revert st)
        | _ -> revert st)
     | _ -> revert st)
  | SOFTMAX_FP (rs_dst, rs_scores, rs_count) ->
    (match read_int st rs_dst, read_int st rs_scores, read_int st rs_count with
     | Some dst, Some scores, Some count
       when count > 0 && count <= 8192
            && valid_large_mem_span dst count
            && valid_large_mem_span scores count
            && (not (ranges_overlap dst count scores count)
                || same_range dst count scores count) ->
       if not (add_dyn_product st [count; 8] 1) then
         revert st
       else
         (match read_fp64_bits_array st.memory.data scores count with
         | Some score_values ->
            let max_score_bits = ref (Array.unsafe_get score_values 0) in
            let ok = ref true in
            for index = 1 to count - 1 do
              let value_bits = Array.unsafe_get score_values index in
              match Inference_fp64.compare value_bits !max_score_bits with
              | Some cmp when cmp > 0 -> max_score_bits := value_bits
              | Some _ -> ()
              | None -> ok := false
            done;
            let exps = Array.make count 0L in
            let sum_exp_bits = ref 0L in
            for index = 0 to count - 1 do
              match
                Inference_fp64.sub
                  (Array.unsafe_get score_values index)
                  !max_score_bits
              with
              | Some shifted_bits ->
                (match Inference_fp64.compare shifted_bits 0L with
                 | Some cmp when cmp <= 0 ->
                   (match Inference_fp64.exp_nonpositive shifted_bits with
                    | Some value_bits ->
                      Array.unsafe_set exps index value_bits;
                      (match Inference_fp64.add !sum_exp_bits value_bits with
                       | Some next -> sum_exp_bits := next
                       | None -> ok := false)
                    | None -> ok := false)
                 | _ -> ok := false)
              | None -> ok := false
            done;
            if (not !ok) || not (fp64_positive_bits !sum_exp_bits) then
              revert st
            else begin
              let output = Array.make count None in
              for index = 0 to count - 1 do
                match
                  Inference_fp64.div
                    (Array.unsafe_get exps index)
                    !sum_exp_bits
                with
                | Some value ->
                  Array.unsafe_set output index (Some value)
                | None -> ok := false
              done;
              if not !ok then
                revert st
              else begin
                for index = 0 to count - 1 do
                  mem_set_fp64_bits
                    st.memory.data
                    (dst + index)
                    (Option.get (Array.unsafe_get output index))
                done;
                true
              end
            end
          | None -> revert st)
     | _ -> revert st)
  | ATTENTION_WEIGHTED_SUM_FP (rs_dst, rs_probs, rs_value, rs_key_count, rs_head_dim) ->
    (match read_int st rs_dst, read_int st rs_probs, read_int st rs_value,
           read_int st rs_key_count, read_int st rs_head_dim with
     | Some dst, Some probs, Some value, Some key_count, Some head_dim
       when key_count > 0 && key_count <= 8192
            && head_dim > 0 && head_dim <= 1024 ->
       (match checked_product key_count head_dim with
        | Some value_cells
          when valid_large_mem_span dst head_dim
               && valid_large_mem_span probs key_count
               && valid_large_mem_span value value_cells
               && not (ranges_overlap dst head_dim probs key_count)
               && not (ranges_overlap dst head_dim value value_cells) ->
          if not (add_dyn_product st [key_count; head_dim; 4] 1) then
            revert st
          else
            (match read_fp64_bits_array st.memory.data probs key_count,
                   read_fp64_bits_array st.memory.data value value_cells with
             | Some prob_values, Some value_values ->
               let output = Array.make head_dim 0L in
               let ok = ref true in
               for dim = 0 to head_dim - 1 do
                 let acc = ref 0L in
                 for key_index = 0 to key_count - 1 do
                   match
                     Inference_fp64.mul
                       (Array.unsafe_get prob_values key_index)
                       (Array.unsafe_get
                          value_values
                          ((key_index * head_dim) + dim))
                   with
                   | Some product ->
                     (match Inference_fp64.add !acc product with
                      | Some next -> acc := next
                      | None -> ok := false)
                   | None -> ok := false
                 done;
                 Array.unsafe_set output dim !acc
               done;
               if not !ok then
                 revert st
               else begin
                 for index = 0 to head_dim - 1 do
                   mem_set_fp64_bits
                     st.memory.data
                     (dst + index)
                     (Array.unsafe_get output index)
                 done;
                 true
               end
             | _ -> revert st)
        | _ -> revert st)
     | _ -> revert st)
  | ATTENTION_KV_FP (rs_q, rs_k, rs_v, rs_ctx, rs_T, rs_n_q_heads, rs_n_kv_heads, rs_head_dim) ->
    let q_addr = Z.to_int (to_z (getr st rs_q)) in
    let k_addr = Z.to_int (to_z (getr st rs_k)) in
    let v_addr = Z.to_int (to_z (getr st rs_v)) in
    let ctx_addr = Z.to_int (to_z (getr st rs_ctx)) in
    let t_total = Z.to_int (to_z (getr st rs_T)) in
    let n_q_heads = Z.to_int (to_z (getr st rs_n_q_heads)) in
    let n_kv_heads = Z.to_int (to_z (getr st rs_n_kv_heads)) in
    let head_dim = Z.to_int (to_z (getr st rs_head_dim)) in
    if t_total <= 0 || t_total > 8192 || n_q_heads <= 0 || n_q_heads > 256
       || n_kv_heads <= 0 || n_kv_heads > 256 || head_dim <= 0 || head_dim > 1024
       || n_q_heads mod n_kv_heads <> 0 then revert st
    else begin
      if not (add_dyn_product st [n_q_heads; t_total; head_dim; 4] 1) then revert st
      else begin
        let kv_dim = n_kv_heads * head_dim in
        let group = n_q_heads / n_kv_heads in
        let inv_sqrt_d = 1.0 /. sqrt (float_of_int head_dim) in
        let scores = Array.make t_total 0.0 in
        for q_head = 0 to n_q_heads - 1 do
          let kv_head = q_head / group in
          let q_off = q_head * head_dim in
          for t = 0 to t_total - 1 do
            let k_off = t * kv_dim + kv_head * head_dim in
            let acc = ref 0.0 in
            for d = 0 to head_dim - 1 do
              let qv = mem_get_fp64 st.memory.data (q_addr + q_off + d) in
              let kv = mem_get_fp64 st.memory.data (k_addr + k_off + d) in
              acc := !acc +. (qv *. kv)
            done;
            scores.(t) <- !acc *. inv_sqrt_d
          done;
          let max_score = ref neg_infinity in
          for t = 0 to t_total - 1 do
            if scores.(t) > !max_score then max_score := scores.(t)
          done;
          let sum_exp = ref 0.0 in
          for t = 0 to t_total - 1 do
            let e = exp (scores.(t) -. !max_score) in
            scores.(t) <- e;
            sum_exp := !sum_exp +. e
          done;
          let inv_sum = 1.0 /. !sum_exp in
          for t = 0 to t_total - 1 do
            scores.(t) <- scores.(t) *. inv_sum
          done;
          for d = 0 to head_dim - 1 do
            let acc = ref 0.0 in
            for t = 0 to t_total - 1 do
              let v_off = t * kv_dim + kv_head * head_dim + d in
              acc := !acc +. (scores.(t) *. mem_get_fp64 st.memory.data (v_addr + v_off))
            done;
            mem_set_fp64 st.memory.data (ctx_addr + q_off + d) !acc
          done
        done;
        true
      end
    end
  | ATTENTION_KV_Q16 (rs_q, rs_k, rs_v, rs_ctx, rs_T, rs_n_q_heads, rs_n_kv_heads, rs_head_dim) ->
    (match read_int st rs_q, read_int st rs_k, read_int st rs_v, read_int st rs_ctx,
           read_int st rs_T, read_int st rs_n_q_heads, read_int st rs_n_kv_heads,
           read_int st rs_head_dim with
     | Some q_addr, Some k_addr, Some v_addr, Some ctx_addr, Some total_tokens,
       Some query_heads, Some key_heads, Some head_dim
       when total_tokens > 0 && total_tokens <= 8192 && query_heads > 0 &&
            query_heads <= 256 && key_heads > 0 && key_heads <= 256 &&
            head_dim > 0 && head_dim <= 1024 && query_heads mod key_heads = 0 &&
            query_heads <= max_int / head_dim &&
            key_heads <= max_int / head_dim &&
            total_tokens <= max_int / (key_heads * head_dim) ->
       let query_size = query_heads * head_dim in
       let key_size = total_tokens * key_heads * head_dim in
       if not (valid_mem_span q_addr query_size) ||
          not (valid_mem_span k_addr key_size) ||
          not (valid_mem_span v_addr key_size) ||
          not (valid_mem_span ctx_addr query_size) then revert st
       else if not (add_dyn_product st [query_heads; total_tokens; head_dim; 4] 1) then
         revert st
       else
         (match read_q16 st q_addr query_size, read_q16 st k_addr key_size,
                read_q16 st v_addr key_size with
          | Some query, Some key, Some value ->
            (match Fixed_q16.attention query key value total_tokens query_heads
                     key_heads head_dim with
             | Some result -> write_q16 st ctx_addr result; true
             | None -> revert st)
          | _ -> revert st)
     | _ -> revert st)
  | APPEND_VEC_FP (rs_dst, rs_pos, rs_src, rs_n) ->
    let dst = Z.to_int (to_z (getr st rs_dst)) in
    let pos = Z.to_int (to_z (getr st rs_pos)) in
    let src = Z.to_int (to_z (getr st rs_src)) in
    let n = Z.to_int (to_z (getr st rs_n)) in
    if n <= 0 || n > 1_048_576 || pos < 0 || pos > 16_777_216 then revert st
    else begin
      if not (add_dyn_effort st n) then revert st
      else begin
        for i = 0 to n - 1 do
          let v = mem_get_fp64 st.memory.data (src + i) in
          mem_set_fp64 st.memory.data (dst + pos * n + i) v
        done;
        true
      end
    end
  | ARGMAX_FP (rd, rs_addr, rs_n) ->
    (match read_int st rs_addr, read_int st rs_n with
     | Some addr, Some n when valid_large_mem_span addr n ->
       if not (add_dyn_effort st (n / 2)) then revert st
       else
         (match read_fp64_bits_array st.memory.data addr n with
          | Some values ->
            let best = ref 0 in
            let ok = ref true in
            for index = 1 to n - 1 do
              match Inference_fp64.compare values.(index) values.(!best) with
              | Some cmp when cmp > 0 -> best := index
              | Some _ -> ()
              | None -> ok := false
            done;
            if not !ok then
              revert st
            else begin
              setr st rd (VInt (Z.of_int !best));
              true
            end
          | None -> revert st)
     | _ -> revert st)
  | JMP addr ->
    st.pc <- addr; true
  | JIF (rs, addr) ->
    if to_bool (getr st rs) then st.pc <- addr; true
  | JDEST _ -> true
  | STOP ->
    (match st.return_stack with
     | [] -> false
     | (ret_pc, dest_r, saved_regs) :: rest ->
       let result = st.regs.(0) in
       Array.blit saved_regs 0 st.regs 0 64;
       st.regs.(dest_r) <- result;
       st.return_stack <- rest;
       st.pc <- ret_pc;
       true)
  | REVERT -> revert st
  | CALLER rd -> setr st rd (VAddr st.caller); true
  | ORIGIN rd -> setr st rd (VAddr st.origin); true
  | SELF rd -> setr st rd (VAddr st.address); true
  | EPOCH rd -> setr st rd (VInt (Z.of_int st.ctx.current_epoch)); true
  | EPOCH_TIME rd -> setr st rd (VInt (Z.of_int64 st.ctx.epoch_time_ms)); true
  | VALUE rd -> setr st rd (VInt st.value); true
  | BALANCE (rd, rs) ->
    (match getr st rs with
     | VAddr addr | VString addr -> setr st rd (VInt (st.ctx.get_balance addr)); true
     | _ -> setr st rd (VInt Z.zero); true)
  | TREEHASH rd -> setr st rd (VString st.ctx.tree_hash); true
  | NODEID rd -> setr st rd (VString st.ctx.node_id); true
  | TXHASH rd -> setr st rd (VString st.ctx.tx_hash); true
  | XCALL (rd, rt, rm, ra, nargs) ->
    if not (valid_reg_span ra nargs) then revert st
    else if st.call_depth >= 8 then revert st
    else
      let target = to_string (getr st rt) in
      let method_name = to_string (getr st rm) in
      let args = List.init nargs (fun i -> getr st (ra + i)) in
      (match st.ctx.call_contract st.address target method_name args (st.call_depth + 1) with
       | Ok sub ->
         if not (add_dyn_effort st sub.effort_used) then revert st
         else begin
           st.logs := List.rev_append sub.events !(st.logs);
           setr st rd sub.return_value;
           true
         end
       | Error _ -> revert st)
  | SPAWN (rd, rs) ->
    if not (view_guard st) then false
    else if st.call_depth >= 8 then revert st
    else
      let input = to_string (getr st rs) in

      let bytecode_raw =
        if String.length input >= 4 && String.sub input 0 4 = "OCTB" then input
        else (try Base64.decode_exn input with _ -> input) in
      if String.length bytecode_raw < 12 then revert st
      else
        let nonce_key = "\x00spawn_nonce" in
        let nonce = match Hashtbl.find_opt st.storage nonce_key with
          | Some s -> (try int_of_string s with _ -> 0) | None -> 0 in
        Hashtbl.replace st.storage nonce_key (string_of_int (nonce + 1));

        let spawn_effort = 5000 + (String.length bytecode_raw / 100) in
        if not (add_dyn_effort st spawn_effort) then revert st
        else
        (match st.ctx.deploy_contract st.address bytecode_raw nonce (st.call_depth + 1) [] with
         | Ok sp ->
           if not (add_dyn_effort st sp.effort_used) then revert st
           else begin
             st.logs := List.rev_append sp.events !(st.logs);
             setr st rd (VAddr sp.spawned_addr);
             true
           end
         | Error e ->
           Octra_log.warn "program"
             "event = spawn_reverted error = %s bytecode_bytes = %d"
             e (String.length bytecode_raw);
           Hashtbl.replace st.storage nonce_key (string_of_int nonce); revert st)
  | SPAWN2 (rd, rs, base, nargs) ->
    if not (valid_reg_span base nargs) then revert st
    else if not (view_guard st) then false
    else if st.call_depth >= 8 then revert st
    else
      let input = to_string (getr st rs) in
      let bytecode_raw =
        if String.length input >= 4 && String.sub input 0 4 = "OCTB" then input
        else (try Base64.decode_exn input with _ -> input) in
      if String.length bytecode_raw < 12 then revert st
      else
        let params = List.init nargs (fun i -> getr st (base + i)) in
        let nonce_key = "\x00spawn_nonce" in
        let nonce = match Hashtbl.find_opt st.storage nonce_key with
          | Some s -> (try int_of_string s with _ -> 0) | None -> 0 in
        Hashtbl.replace st.storage nonce_key (string_of_int (nonce + 1));
        let spawn_effort = 5000 + (String.length bytecode_raw / 100) in
        if not (add_dyn_effort st spawn_effort) then revert st
        else
        (match st.ctx.deploy_contract st.address bytecode_raw nonce (st.call_depth + 1) params with
         | Ok sp ->
           if not (add_dyn_effort st sp.effort_used) then revert st
           else begin
             st.logs := List.rev_append sp.events !(st.logs);
             setr st rd (VAddr sp.spawned_addr);
             true
           end
         | Error e ->
           Octra_log.warn "program"
             "event = spawn_reverted version = 2 error = %s bytecode_bytes = %d params = %d"
             e (String.length bytecode_raw) nargs;
           Hashtbl.replace st.storage nonce_key (string_of_int nonce); revert st)
  | TRANSFER (rd, ra, rv) ->
    if not (view_guard st) then false
    else
      let to_addr = to_string (getr st ra) in
      if not (is_valid_addr to_addr) then (setr st rd (VBool false); true)
      else
        let amount = to_z (getr st rv) in

        if Z.sign amount < 0 then (setr st rd (VBool false); true)
        else if Z.equal amount Z.zero then (setr st rd (VBool true); true)
        else
          let ok = st.ctx.do_transfer st.address to_addr amount in
          setr st rd (VBool ok); true
  | CHECKPOINT ->
    st.undo_id <- st.undo_id + 1;
    st.undo_stack <- UndoMarker st.undo_id :: st.undo_stack;
    true
  | ROLLBACK ->
    let rec restore = function
      | [] -> []
      | UndoMarker _ :: rest -> rest
      | UndoWrite (k, Some v) :: rest ->
        Hashtbl.replace st.storage k v; restore rest
      | UndoWrite (k, None) :: rest ->
        Hashtbl.remove st.storage k; restore rest
    in
    st.undo_stack <- restore st.undo_stack;
    true
  | COMMIT ->
    let rec discard = function
      | [] -> []
      | UndoMarker _ :: rest -> rest
      | _ :: rest -> discard rest
    in
    st.undo_stack <- discard st.undo_stack;
    true
  | EMIT (event, regs_list) ->
    if List.length !(st.logs) >= 256 then revert st
    else
    let vals = List.map (fun r -> getr st r) regs_list in
    st.logs := { contract = st.address; depth = st.call_depth; event; values = vals }
               :: !(st.logs);
    true
  | CONCAT (rd, rs1, rs2) ->
    setr st rd (VString (to_string (getr st rs1) ^ to_string (getr st rs2))); true
  | STRLEN (rd, rs) ->
    setr st rd (VInt (Z.of_int (String.length (to_string (getr st rs))))); true
  | ASSERT rs ->
    if not (to_bool (getr st rs)) then revert st else true
  | EFFORT rd ->
    setr st rd (VInt (Z.of_int st.effort_used)); true
  | NOP -> true
  | FHE_LOAD_PK (rd, rs) ->
    if not (st.ctx.allow_fhe_capability Fhe_load_pk_cap) then
      revert_with_reason st "fhe_load_pk not allowed"
    else
      let addr = to_string (getr st rs) in
      (match st.ctx.get_fhe_pubkey addr with
       | Some pk -> setr st rd (VPubKey pk); true
       | None -> revert_with_reason st ("fhe pubkey not available: " ^ addr))
  | FHE_ADD (rd, rpk, ra, rb) ->
    if not (st.ctx.allow_fhe_capability Fhe_cipher_arithmetic_cap) then
      revert st
    else
      (match to_pubkey (getr st rpk), to_cipher (getr st ra), to_cipher (getr st rb) with
       | Some pk, Some a, Some b ->
         (try setr st rd (VCipher (Pvac_ffi.ct_add pk a b)); true
          with _ -> revert st)
       | _ -> revert st)
  | FHE_SUB (rd, rpk, ra, rb) ->
    if not (st.ctx.allow_fhe_capability Fhe_cipher_arithmetic_cap) then
      revert st
    else
      (match to_pubkey (getr st rpk), to_cipher (getr st ra), to_cipher (getr st rb) with
       | Some pk, Some a, Some b ->
         (try setr st rd (VCipher (Pvac_ffi.ct_sub pk a b)); true
          with _ -> revert st)
       | _ -> revert st)
  | FHE_MUL (rd, rpk, ra, rb) ->
    if not (st.ctx.allow_fhe_capability Fhe_cipher_arithmetic_cap) then
      revert st
    else
      (match to_pubkey (getr st rpk), to_cipher (getr st ra), to_cipher (getr st rb) with
       | Some pk, Some a, Some b ->
         (try
            let seed = deterministic_seed [
              "aml.fhe_mul";
              st.ctx.tx_hash;
              st.address;
              string_of_int st.pc;
              string_of_int rd;
            ] in
            setr st rd (VCipher (Pvac_ffi.ct_mul_seeded pk a b seed)); true
          with _ -> revert st)
       | _ -> revert st)
  | FHE_SCALE (rd, rpk, rct, rscalar) ->
    if not (st.ctx.allow_fhe_capability Fhe_cipher_arithmetic_cap) then
      revert st
    else
      (match to_pubkey (getr st rpk), to_cipher (getr st rct) with
       | Some pk, Some ct ->
         (try
            let s = Z.to_int64 (to_z (getr st rscalar)) in
            setr st rd (VCipher (Pvac_ffi.ct_scale pk ct s)); true
          with _ -> revert st)
       | _ -> revert st)
  | FHE_DIV_CONST (rd, rpk, rct, rdivisor) ->
    if not (st.ctx.allow_fhe_capability Fhe_cipher_arithmetic_cap) then
      revert st
    else
      (match to_pubkey (getr st rpk), to_cipher (getr st rct) with
       | Some pk, Some ct ->
         let divisor = to_z (getr st rdivisor) in
         if Z.sign divisor <= 0 || Z.gt divisor (Z.of_int64 Int64.max_int) then
           revert_with_reason st "fhe_div_const divisor out of range"
         else
           (try
              let lo = Z.to_int64 divisor in
              setr st rd (VCipher (Pvac_ffi.ct_div_const pk ct lo 0L)); true
            with _ -> revert st)
       | _ -> revert st)
  | FHE_ADD_CONST (rd, rpk, rct, rconst) ->
    if not (st.ctx.allow_fhe_capability Fhe_cipher_arithmetic_cap) then
      revert st
    else
      (match to_pubkey (getr st rpk), to_cipher (getr st rct) with
       | Some pk, Some ct ->
         (try
            let c = Z.to_int64 (to_z (getr st rconst)) in
            setr st rd (VCipher (Pvac_ffi.ct_add_const pk ct c 0L)); true
          with _ -> revert st)
       | _ -> revert st)
  | FHE_SUB_CONST (rd, rpk, rct, rconst) ->
    if not (st.ctx.allow_fhe_capability Fhe_cipher_arithmetic_cap) then
      revert st
    else
      (match to_pubkey (getr st rpk), to_cipher (getr st rct) with
       | Some pk, Some ct ->
         (try
            let c = Z.to_int64 (to_z (getr st rconst)) in
            setr st rd (VCipher (Pvac_ffi.ct_sub_const pk ct c)); true
          with _ -> revert st)
       | _ -> revert st)
  | FHE_VERIFY_ZERO (rd, rpk, rct, rproof) ->
    if not (st.ctx.allow_fhe_capability Fhe_verify_zero_cap) then
      revert st
    else if not st.is_view then
      revert st
    else
      (match to_pubkey (getr st rpk), to_cipher (getr st rct), to_bytes (getr st rproof) with
       | Some pk, Some ct, Some proof_bytes
         when fhe_verifier_cipher_allowed ct ->
         let raw_len = match Base64.decode proof_bytes with
           | Ok r -> String.length r | Error _ -> String.length proof_bytes in
         if raw_len > max_fhe_proof_bytes then
           (setr st rd (VBool false); true)
         else
           let ok =
             with_fhe_verifier_lane (fun () ->
               match worker_zero_proof proof_bytes with
               | None -> false
               | Some proof ->
                 begin
                   match
                     Octra_core.Pvac_verify_worker.verify_zero_sync
                       ~pubkey:(encoded_worker_pubkey pk)
                       ~cipher:(encoded_worker_cipher ct)
                       ~proof
                   with
                   | Ok () -> true
                   | Error _ -> false
                 end)
           in
           setr st rd (VBool ok);
           true
       | _ -> revert st)
  | FHE_VERIFY_RANGE (rd, rpk, rct, rproof) ->
    if not (st.ctx.allow_fhe_capability Fhe_verify_range_cap) then
      revert st
    else if not st.is_view then
      revert st
    else
    (match to_pubkey (getr st rpk), to_cipher (getr st rct), to_bytes (getr st rproof) with
     | Some pk, Some ct, Some proof_bytes
       when fhe_verifier_cipher_allowed ct ->
       let raw_len = match Base64.decode proof_bytes with
         | Ok r -> String.length r | Error _ -> String.length proof_bytes in
       if raw_len > max_fhe_proof_bytes then
         (setr st rd (VBool false); true)
       else
         let ok =
           with_fhe_verifier_lane (fun () ->
             match worker_range_proof proof_bytes with
             | None -> false
             | Some proof ->
               begin
                 match
                   Octra_core.Pvac_verify_worker.verify_range_sync
                     ~pubkey:(encoded_worker_pubkey pk)
                     ~cipher:(encoded_worker_cipher ct)
                     ~proof
                 with
                 | Ok () -> true
                 | Error _ -> false
               end)
         in
         setr st rd (VBool ok);
         true
     | _ -> revert st)
  | GROTH16_VERIFY_BN254 (rd, rvk, rproof, rinputs) ->
    if not st.is_view then
      revert st
    else
    (match to_bytes (getr st rvk), to_bytes (getr st rproof), to_bytes (getr st rinputs) with
     | Some vk_in, Some proof_in, Some inputs_in ->
       let decode_b64 b =
         match Base64.decode b with Ok r -> r | Error _ -> b
       in
       let vk_raw = decode_b64 vk_in in
       let proof_raw = decode_b64 proof_in in
       let inputs_raw = decode_b64 inputs_in in
       if String.length vk_raw > max_zk_vk_bytes
          || String.length proof_raw > max_zk_proof_bytes
          || String.length inputs_raw > max_zk_inputs_bytes then
         (setr st rd (VBool false); true)
       else
         (try
            let ok = Zk_ffi.groth16_verify_bn254
              (Bytes.of_string vk_raw)
              (Bytes.of_string proof_raw)
              (Bytes.of_string inputs_raw) in
            setr st rd (VBool ok); true
          with _ -> setr st rd (VBool false); true)
     | _ -> revert st)
  | FHE_VERIFY_BOUND (rd, rpk, rct, rproof, rcommit) ->
    if not (st.ctx.allow_fhe_capability Fhe_verify_bound_cap) then
      revert st
    else if not st.is_view then
      revert st
    else
      (match to_pubkey (getr st rpk), to_cipher (getr st rct),
             to_bytes (getr st rproof), to_bytes (getr st rcommit) with
       | Some pk, Some ct, Some proof_bytes, Some commit_bytes
         when fhe_verifier_cipher_allowed ct ->
         let raw_len = match Base64.decode proof_bytes with
           | Ok r -> String.length r | Error _ -> String.length proof_bytes in
         if raw_len > max_fhe_proof_bytes then
           (setr st rd (VBool false); true)
         else
           let ok =
             with_fhe_verifier_lane (fun () ->
               match
                 worker_zero_proof proof_bytes,
                 worker_commitment commit_bytes
               with
               | Some proof, Some commitment ->
                 begin
                   match
                     Octra_core.Pvac_verify_worker.verify_claim_sync
                       ~pubkey:(encoded_worker_pubkey pk)
                       ~cipher:(encoded_worker_cipher ct)
                       ~proof
                       ~commitment
                   with
                   | Ok () -> true
                   | Error _ -> false
                 end
               | _ -> false)
           in
           setr st rd (VBool ok);
           true
       | _ -> revert st)
  | FHE_COMMIT (rd, rpk, rct) ->
    if not (st.ctx.allow_fhe_capability Fhe_commit_cap) then
      revert st
    else
      (match to_pubkey (getr st rpk), to_cipher (getr st rct) with
       | Some pk, Some ct ->
         (try
            let raw = Bytes.to_string (Pvac_ffi.commit_ct pk ct) in
            setr st rd (VString (Base64.encode_exn raw)); true
          with _ -> revert st)
       | _ -> revert st)
  | FHE_PEDERSEN (rd, ramount, rblinding) ->
    if not (st.ctx.allow_fhe_capability Fhe_pedersen_cap) then
      revert st
    else
      (match to_bytes (getr st rblinding) with
       | Some blinding ->
         (try
            let amount = Z.to_int64 (to_z (getr st ramount)) in
            let result = Pvac_ffi.pedersen_commit_amount amount (Bytes.of_string blinding) in
            setr st rd (VString (Base64.encode_exn (Bytes.to_string result))); true
          with _ -> revert st)
       | _ -> revert st)
  | FHE_SER (rd, rct) ->
    if not (st.ctx.allow_fhe_capability Fhe_cipher_serde_cap) then
      revert st
    else
      (match to_cipher (getr st rct) with
       | Some ct ->
         (try
            let raw = Bytes.to_string (Pvac_ffi.serialize_cipher ct) in
            setr st rd (VString (Base64.encode_exn raw)); true
          with _ -> revert st)
       | None -> revert st)
  | FHE_DESER (rd, rbytes) ->
    if not (st.ctx.allow_fhe_capability Fhe_cipher_serde_cap) then
      revert st
    else
      (match to_bytes (getr st rbytes) with
       | Some encoded
         when add_dyn_effort st (fhe_decode_input_cost encoded) ->
         let raw = decode_serialized_text encoded in
         if String.length raw > max_fhe_cipher_bytes then
           revert st
         else
           (try
              let ct = Pvac_ffi.deserialize_cipher (Bytes.of_string raw) in
              setr st rd (VCipher ct);
              true
            with _ ->
              revert st)
       | Some _ -> revert st
       | None -> revert st)
  | FHE_SER_PK (rd, rpk) ->
    if not (st.ctx.allow_fhe_capability Fhe_pubkey_serde_cap) then
      revert st
    else
      (match to_pubkey (getr st rpk) with
       | Some pk ->
         (try
            let raw = Bytes.to_string (Pvac_ffi.serialize_pubkey pk) in
            setr st rd (VString (Base64.encode_exn raw)); true
          with _ -> revert st)
       | None -> revert st)
  | FHE_DESER_PK (rd, rbytes) ->
    if not (st.ctx.allow_fhe_capability Fhe_pubkey_serde_cap) then
      revert st
    else
      (match to_bytes (getr st rbytes) with
       | Some encoded
         when add_dyn_effort st (fhe_decode_input_cost encoded) ->
         let raw = decode_serialized_text encoded in
         if String.length raw > max_fhe_pubkey_bytes then
           revert st
         else
           (match fhe_pubkey_output_cost raw with
            | Some cost when add_dyn_effort st cost ->
              (try
                 let pk = Pvac_ffi.deserialize_pubkey (Bytes.of_string raw) in
                 setr st rd (VPubKey pk);
                 true
               with _ ->
                 revert st)
            | Some _ | None ->
              revert st)
       | Some _ -> revert st
       | None -> revert st)
  | CALL_INT (rd, label) ->
    if List.length st.return_stack >= 8 then revert st
    else begin
      let saved = Array.copy st.regs in
      st.return_stack <- (st.pc, rd, saved) :: st.return_stack;
      st.pc <- label;
      true
    end

type opcode_profile = {
  opcode : string;
  count : int;
  effort_used : int;
  microseconds : int;
}

type opcode_profile_acc = {
  mutable profile_count : int;
  mutable profile_effort_used : int;
  mutable profile_microseconds : int;
}

let profile_microseconds started stopped =
  int_of_float ((stopped -. started) *. 1_000_000.0)

let profile_record table opcode effort_delta elapsed =
  let acc =
    match Hashtbl.find_opt table opcode with
    | Some acc -> acc
    | None ->
      let acc =
        {
          profile_count = 0;
          profile_effort_used = 0;
          profile_microseconds = 0;
        }
      in
      Hashtbl.replace table opcode acc;
      acc
  in
  acc.profile_count <- acc.profile_count + 1;
  acc.profile_effort_used <- acc.profile_effort_used + effort_delta;
  acc.profile_microseconds <- acc.profile_microseconds + elapsed

let profile_rows table =
  Hashtbl.fold
    (fun opcode acc rows ->
      {
        opcode;
        count = acc.profile_count;
        effort_used = acc.profile_effort_used;
        microseconds = acc.profile_microseconds;
      } :: rows)
    table
    []
  |> List.sort
       (fun left right ->
         match compare right.microseconds left.microseconds with
         | 0 -> String.compare left.opcode right.opcode
         | order -> order)

let run_with_step state program step =
  try
    let len = Array.length program in
    while state.pc < len && not state.reverted do
      let op = program.(state.pc) in
      state.pc <- state.pc + 1;
      if not (step state op) then state.pc <- len
    done;
    not state.reverted
  with
  | Z.Overflow
  | Invalid_argument _
  | Failure _
  | Not_found
  | Division_by_zero ->
    state.reverted <- true;
    false

let run state program =
  run_with_step state program exec_one

let run_profiled ~clock ~opcode_name state program =
  let profile = Hashtbl.create 64 in
  let step (state : s) op =
    let opcode = opcode_name op in
    let effort_before = state.effort_used in
    let started = clock () in
    let ok = exec_one state op in
    let stopped = clock () in
    profile_record
      profile
      opcode
      (state.effort_used - effort_before)
      (profile_microseconds started stopped);
    ok
  in
  let success = run_with_step state program step in
  success, profile_rows profile

module Verifier = struct
  type err =
    | InvalidReg of int * int
    | InvalidRegSpan of int * int * int
    | InvalidJumpDest of int
    | DuplicateJDest of int
    | CodeTooLarge of int
    | EmptyCode
    | ReservedKey of int * string

  let max_size = 33_554_432

  let check_reg pc r =
    if r < 0 || r > 63 then Some (InvalidReg (pc, r)) else None

  let check_regs pc regs =
    List.find_map (check_reg pc) regs

  let check_reg_span pc base count =
    if not (valid_reg_span base count) then
      Some (InvalidRegSpan (pc, base, count))
    else None

  let verify code =
    if Array.length code = 0 then Error EmptyCode
    else if Array.length code * 32 > max_size then Error (CodeTooLarge (Array.length code))
    else
      let dests = Hashtbl.create 32 in
      let dup = ref None in
      Array.iter (fun op -> match op with
        | JDEST n ->
          if Hashtbl.mem dests n then dup := Some n;
          Hashtbl.replace dests n true
        | _ -> ()
      ) code;
      match !dup with
      | Some n -> Error (DuplicateJDest n)
      | None ->
      let rec check pc =
        if pc >= Array.length code then Ok ()
        else
          let err = match code.(pc) with
            | ADD (d,a,b) | SUB (d,a,b) | MUL (d,a,b)
            | DIV (d,a,b) | MOD (d,a,b)
            | EQ (d,a,b) | LT (d,a,b) | GT (d,a,b) | NEQ (d,a,b)
            | CONCAT (d,a,b) -> check_regs pc [d;a;b]
            | NEG (d,s) | ABS (d,s) | MOV (d,s) | BALANCE (d,s)
            | SLOADK (d,s) | SSTOREK (d,s) | SPAWN (d,s)
            | STRLEN (d,s) -> check_regs pc [d;s]
            | SDELK s -> check_reg pc s
            | LDI (d,_) | SLOAD (d,_) | MLOAD (d,_)
            | CALLER d | ORIGIN d | SELF d | EPOCH d | EPOCH_TIME d | VALUE d
            | TREEHASH d | NODEID d | TXHASH d | EFFORT d -> check_reg pc d
            | SSTORE (k,s) ->
              if is_reserved_key k then Some (ReservedKey (pc, k))
              else check_reg pc s
            | SDEL k ->
              if is_reserved_key k then Some (ReservedKey (pc, k))
              else None
            | MSTORE (_,s) | ASSERT s -> check_reg pc s
            | TRANSFER (d,a,v) -> check_regs pc [d;a;v]
            | XCALL (d,t,m,a,n) -> check_regs pc [d;t;m] |> (function
              | Some e -> Some e
              | None -> check_reg_span pc a n)
            | SPAWN2 (d,s,a,n) -> check_regs pc [d;s] |> (function
              | Some e -> Some e
              | None -> check_reg_span pc a n)
            | FHE_ADD (d,pk,a,b) | FHE_SUB (d,pk,a,b) | FHE_MUL (d,pk,a,b)
            | FHE_SCALE (d,pk,a,b) | FHE_DIV_CONST (d,pk,a,b)
            | FHE_ADD_CONST (d,pk,a,b)
            | FHE_SUB_CONST (d,pk,a,b)
            | FHE_VERIFY_ZERO (d,pk,a,b) | FHE_VERIFY_RANGE (d,pk,a,b) ->
              check_regs pc [d;pk;a;b]
            | GROTH16_VERIFY_BN254 (d,vk,pf,inp) -> check_regs pc [d;vk;pf;inp]
            | FHE_VERIFY_BOUND (d,pk,ct,pf,cm) -> check_regs pc [d;pk;ct;pf;cm]
            | FHE_COMMIT (d,pk,ct) | FHE_PEDERSEN (d,pk,ct) -> check_regs pc [d;pk;ct]
            | FHE_LOAD_PK (d,s) | FHE_SER (d,s) | FHE_DESER (d,s)
            | FHE_SER_PK (d,s) | FHE_DESER_PK (d,s)
            | MLOADR (d,s) | MSTORER (d,s) -> check_regs pc [d;s]
            | PARSE_INTS (d,a,b) -> check_regs pc [d;a;b]
            | ISADDR (d,s) -> check_regs pc [d;s]
            | ISHEX (d,s) -> check_regs pc [d;s]
            | STATE_PATH_KEY (d,s) -> check_regs pc [d;s]
            | OBJECT_MEMBER_COUNT (d,s) -> check_regs pc [d;s]
            | OBJECT_HAS_MEMBER (d,a,b)
            | OBJECT_MEMBER_REF_AT (d,a,b) -> check_regs pc [d;a;b]
            | OBJECT_TRANSITION_APPLY (d,a,b,c,e,f,g,h,i,j,k) ->
              check_regs pc [d;a;b;c;e;f;g;h;i;j;k]
            | ASSERT_ADDR s -> check_reg pc s
            | SUBSTR (d,s,a,b) -> check_regs pc [d;s;a;b]
            | INDEXOF (d,s,p) -> check_regs pc [d;s;p]
            | SHA256 (d,s) | KECCAK256 (d,s) -> check_regs pc [d;s]
            | ED25519_OK (d,pk,msg,sig_) -> check_regs pc [d;pk;msg;sig_]
            | BITAND (d,a,b) | BITOR (d,a,b) | BITXOR (d,a,b)
            | BITSHL (d,a,b) | BITSHR (d,a,b) -> check_regs pc [d;a;b]
            | SKEYS (d,p,b) -> check_regs pc [d;p;b]
            | SKEYS_PAGE (d,n,p,a,b) -> check_regs pc [d;n;p;a;b]
            | SLOADN (a,b,c) -> check_regs pc [a;b;c]
            | SSTOREN (a,b,c) -> check_regs pc [a;b;c]
            | FSTORE (d,s) -> check_regs pc [d;s]
            | FLOAD (d,s) -> check_regs pc [d;s]
            | MATMUL (d,l,r,m,k,n) -> check_regs pc [d;l;r;m;k;n]
            | VECDOT (d,a,b,n) -> check_regs pc [d;a;b;n]
            | VECDOT_Q16 (d,a,b,n) -> check_regs pc [d;a;b;n]
            | EXP_LUT (d,x) -> check_regs pc [d;x]
            | EXP_Q16 (d,x) -> check_regs pc [d;x]
            | SOFTMAX_INPLACE (a,n) -> check_regs pc [a;n]
            | SOFTMAX_Q16_INPLACE (a,n) -> check_regs pc [a;n]
            | LAYERNORM_INPLACE (a,n,g,b) -> check_regs pc [a;n;g;b]
            | LAYERNORM_Q16_INPLACE (a,n,g,b) -> check_regs pc [a;n;g;b]
            | RELU_INPLACE (a,n) -> check_regs pc [a;n]
            | RMSNORM_INPLACE (a,n,g) -> check_regs pc [a;n;g]
            | RMSNORM_Q16_INPLACE (a,n,g) -> check_regs pc [a;n;g]
            | SILU_INPLACE (a,n) -> check_regs pc [a;n]
            | SILU_Q16_INPLACE (a,n) -> check_regs pc [a;n]
            | ELEMWISE_MUL_INPLACE (d,s,n) -> check_regs pc [d;s;n]
            | ELEMWISE_MUL_Q16 (d,s,n) -> check_regs pc [d;s;n]
            | LOAD_INT8_BYTES_TO_MEM (d,s,o,n,sc) -> check_regs pc [d;s;o;n;sc]
            | RESIDUAL_ADD (d,s,n) -> check_regs pc [d;s;n]
            | RESIDUAL_ADD_Q16 (d,s,n) -> check_regs pc [d;s;n]
            | ROPE_APPLY (a,n,p,b) -> check_regs pc [a;n;p;b]
            | ROPE_APPLY_Q16 (a,n,p,b) -> check_regs pc [a;n;p;b]
            | LOAD_INT8_B64_TO_MEM (d,s,o,n,sc) -> check_regs pc [d;s;o;n;sc]
            | LOAD_INT8_Q16 (d,s,o,n,sc) -> check_regs pc [d;s;o;n;sc]
            | APPEND_VEC_Q16 (d,p,s,n) -> check_regs pc [d;p;s;n]
            | ARGMAX_Q16 (d,a,n) -> check_regs pc [d;a;n]
            | MATMUL_Q16 (d,l,r,m,k,n) -> check_regs pc [d;l;r;m;k;n]
            | LINEAR_Q1_G128_FP (d,l,q,o,m,k,n) -> check_regs pc [d;l;q;o;m;k;n]
            | LOAD_F32_LE_FP (d,s,o,n) -> check_regs pc [d;s;o;n]
            | LOAD_F64_LE_FP (d,s,o,n) -> check_regs pc [d;s;o;n]
            | SIGMOID_FP (a,n) -> check_regs pc [a;n]
            | SOFTPLUS_FP (a,n) -> check_regs pc [a;n]
            | CAUSAL_DEPTHWISE_CONV1D_FP (d,i,k,t,c,w) -> check_regs pc [d;i;k;t;c;w]
            | GATED_DELTA_RULE_FP
                (o,sd,q,k,v,ld,b,s,t,qh,kh,vh,kd,vd) ->
              check_regs pc [o;sd;q;k;v;ld;b;s;t;qh;kh;vh;kd;vd]
            | SHIFT_ROUND_INPLACE (a,n,b) -> check_regs pc [a;n;b]
            | MATMUL_FP (d,l,r,m,k,n) -> check_regs pc [d;l;r;m;k;n]
            | RMSNORM_FP (a,n,g) -> check_regs pc [a;n;g]
            | RMSNORM_FP_EPS (a,n,g,e) -> check_regs pc [a;n;g;e]
            | L2NORM_FP (a,n,e) -> check_regs pc [a;n;e]
            | SILU_FP (a,n) -> check_regs pc [a;n]
            | ELEMWISE_MUL_FP (d,s,n) -> check_regs pc [d;s;n]
            | RESIDUAL_ADD_FP (d,s,n) -> check_regs pc [d;s;n]
            | ROPE_APPLY_FP (a,n,p,b) -> check_regs pc [a;n;p;b]
            | ROPE_APPLY_INDEXED_FP (a,n,h,r,p,b) ->
              check_regs pc [a;n;h;r;p;b]
            | LOAD_INT8_FP (d,s,o,n,sc) -> check_regs pc [d;s;o;n;sc]
            | VECDOT_FP (d,a,b,n) -> check_regs pc [d;a;b;n]
            | ARGMAX_FP (d,a,n) -> check_regs pc [d;a;n]
            | ATTENTION_SCORES_FP (d,q,k,t,h) -> check_regs pc [d;q;k;t;h]
            | SOFTMAX_FP (d,s,n) -> check_regs pc [d;s;n]
            | ATTENTION_WEIGHTED_SUM_FP (d,p,v,t,h) ->
              check_regs pc [d;p;v;t;h]
            | ATTENTION_KV_FP (q,k,v,c,t,nq,nk,hd) -> check_regs pc [q;k;v;c;t;nq;nk;hd]
            | ATTENTION_KV_Q16 (q,k,v,c,t,nq,nk,hd) -> check_regs pc [q;k;v;c;t;nq;nk;hd]
            | APPEND_VEC_FP (d,p,s,n) -> check_regs pc [d;p;s;n]
            | EMIT (_,rs) -> check_regs pc rs
            | JIF (s, dest) ->
              (match check_reg pc s with
               | Some e -> Some e
               | None ->
                 if not (Hashtbl.mem dests dest) then Some (InvalidJumpDest dest)
                 else None)
            | JMP dest ->
              if not (Hashtbl.mem dests dest) then Some (InvalidJumpDest dest)
              else None
            | CALL_INT (d, dest) ->
              (match check_reg pc d with
               | Some e -> Some e
               | None ->
                 if not (Hashtbl.mem dests dest) then Some (InvalidJumpDest dest)
                 else None)
            | JDEST _ -> None
            | _ -> None
          in
          match err with Some e -> Error e | None -> check (pc + 1)
      in
      check 0
end
